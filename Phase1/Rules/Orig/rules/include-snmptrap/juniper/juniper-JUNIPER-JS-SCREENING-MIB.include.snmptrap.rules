###############################################################################
#
#    Licensed Materials - Property of IBM
#    5724-048
#
#    (C) Copyright IBM Corp. 2008
#    All Rights Reserved
#
#    US Government Users Restricted Rights - Use, duplication or
#    disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
#
#    juniper-JUNIPER-JS-SCREENING-MIB.include.snmptrap.rules 6.0
#    2008/03/07 Anchala Bansal
#
###############################################################################
#
# 6.0 - Updated release for JUNOS 9.0
#
#          - Repackaged for NIM-06
#
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/OMNIbus 3.6 and 7.x
#          -  Netcool Rules File Standards (MUSE-STD-RF-03, Jan 2006)
#          -  JUNIPER-JS-SCREENING-MIB
#
###############################################################################
case ".1.3.6.1.4.1.2636.3.39.1.8.1": ### Juniper Enterprise Firewall Screening - Notifications from JUNIPER-JS-SCREENING-MIB

    log(DEBUG, "<<<<< Entering... juniper-JUNIPER-JS-SCREENING-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Juniper-Enterprise Firewall Screening"
    @Class = "40200"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### jnxJsScreenAttack

            ##########
            # $1 = jnxJsScreenZoneName 
            # $2 = ifName 
            # $3 = jnxJsScreenAttackType 
            # $4 = jnxJsScreenAttackCounter 
            # $5 = jnxJsScreenAttackDescr 
            ##########

            $jnxJsScreenZoneName = $1
            $ifName = $2
            $jnxJsScreenAttackType = lookup($3,jnxJsScreenAttackType) + " ( " + $3 + " )"
            $jnxJsScreenAttackCounter = $4
            $jnxJsScreenAttackDescr = $5
            $ifIndex = extract ($OID2, "\.([0-9]+)$" )

            $OS_EventId = "SNMPTRAP-juniper-JUNIPER-JS-SCREENING-MIB-jnxJsScreenAttack"

            @AlertGroup = "Firewall Screen Status"
            @AlertKey = "jnxJsScreenMonEntry." + $1 + ", " + "ifEntry." + $ifIndex 
            @Summary = "Device Detects Attack due to Rise in Configured Threshold"

            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0 

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($jnxJsScreenZoneName,$ifName,$jnxJsScreenAttackType,$jnxJsScreenAttackCounter,$jnxJsScreenAttackDescr)

        case "2": ### jnxJsScreenCfgChange

            ##########
            # $1 = jnxJsScreenZoneName 
            # $2 = jnxJsScreenAttackType 
            # $3 = jnxJsScreenCfgStatus 
            ##########

            $jnxJsScreenZoneName = $1
            $jnxJsScreenAttackType = lookup($2, jnxJsScreenAttackType) + " ( " + $2 + " )"
            $jnxJsScreenCfgStatus = lookup($3, jnxJsScreenCfgStatus) + " ( " + $3 + " )"

            $OS_EventId = "SNMPTRAP-juniper-JUNIPER-JS-SCREENING-MIB-jnxJsScreenCfgChange"
            
            @AlertGroup = "Firewall Screen Status"
            @AlertKey = "jnxJsScreenMonEntry." + $1
        
            switch($3) ## jnxJsScreenCfgStatus
            {
                case "1": ### disabled
                    $SEV_KEY = $OS_EventId + "_disabled"
                    @Summary = "Firewall Screening Option Disabled"

                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "2": ### enabled
                    $SEV_KEY = $OS_EventId + "_enabled"
                    @Summary = "Firewall Screening Option Enabled"

                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                default: ### unknown
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "Firewall Screening Option Unknown"

                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($jnxJsScreenZoneName,$jnxJsScreenAttackType,$jnxJsScreenCfgStatus)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, juniper-JUNIPER-JS-SCREENING-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, juniper-JUNIPER-JS-SCREENING-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/juniper/juniper-JUNIPER-JS-SCREENING-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/juniper/juniper-JUNIPER-JS-SCREENING-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... juniper-JUNIPER-JS-SCREENING-MIB.include.snmptrap.rules >>>>>")
