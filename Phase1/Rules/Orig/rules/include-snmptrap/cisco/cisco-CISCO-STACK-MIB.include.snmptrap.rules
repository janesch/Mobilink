###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-STACK-MIB.include.snmptrap.rules 2.1
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-STACK-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.5": ### Cisco Catalyst OS - Notifications from CISCO-STACK-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-STACK-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Catalyst OS"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### lerAlarmOn
        
            ##########
            # $1 = fddimibPORTSMTIndex
            # $2 = fddimibPORTIndex
            ##########
            
            $fddimibPORTSMTIndex = $1
            $fddimibPORTIndex = $2
            details($fddimibPORTSMTIndex,$fddimibPORTIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-lerAlarmOn"

            @AlertGroup = "Link Error Rate"
            @AlertKey = "fddimibPORTEntry." + $1 + "." + $2
            @Summary = "Link Error Rate Alarm  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap 

        case "2": ### lerAlarmOff
        
            ##########
            # $1 = fddimibPORTSMTIndex
            # $2 = fddimibPORTIndex 
            ##########
            
            $fddimibPORTSMTIndex = $1
            $fddimibPORTIndex = $2
            details($fddimibPORTSMTIndex,$fddimibPORTIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-lerAlarmOff"

            @AlertGroup = "Link Error Rate"
            @AlertKey = "fddimibPORTEntry." + $1 + "." + $2
            @Summary = "Link Error Rate Alarm Cleared  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap 

        case "3": ### moduleUp
        
            ##########
            # $1 = moduleIndex
            # $2 = moduleType 
            ##########
            
            $moduleIndex = $1
            $moduleType = lookup($2, ciscoStackModuleType)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-moduleUp"

            @AlertGroup = "Module Status"
            @AlertKey = "moduleEntry." + $1
            @Summary = $moduleType + " Module Up  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
            $moduleType = $moduleType + " ( " + $2 + " )"
            details($moduleIndex,$moduleType)

        case "4": ### moduleDown
        
            ##########
            # $1 = moduleIndex 
            # $2 = moduleType
            ##########
            
            $moduleIndex = $1
            $moduleType = lookup($2, ciscoStackModuleType)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-moduleDown"

            @AlertGroup = "Module Status"
            @AlertKey = "moduleEntry." + $1
            @Summary = $moduleType + " Module Down  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
            $moduleType = $moduleType + " ( " + $2 + " )"
            details($moduleIndex,$moduleType)
            
        case "5": ### chassisAlarmOn
        
            ##########
            # $1 = chassisTempAlarm - The chassis minor alarm status.
            # $2 = chassisMinorAlarm - The chassis major alarm status.
            # $3 = chassisMajorAlarm - The chassis temperature alarm status.
            ##########

            $chassisTempAlarm = $1
            $chassisMinorAlarm = $2
            $chassisMajorAlarm = $3
            details($chassisTempAlarm,$chassisMinorAlarm,$chassisMajorAlarm)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-chassisAlarmOn"

            @AlertGroup = "Chassis Status"
            @AlertKey = ""
            if(int($1) > 1)
            {
                switch ($1)
                {
                    case "1": ### off
                        @Summary = "Temperature Alarm Cleared"
                        
                        $SEV_KEY = $OS_EventId + "_tempoff"
                        $DEFAULT_Severity = 1
                        $DEFAULT_Type = 2
                        $DEFAULT_ExpireTime = 0

                    case "2": ### on
                        @Summary = "Temperature Too high"
                        
                        $SEV_KEY = $OS_EventId + "_tempon"
                        $DEFAULT_Severity = 4
                        $DEFAULT_Type = 1
                        $DEFAULT_ExpireTime = 0

                    case "3": ### critical
                        @Summary = "Temperature Critical, System Shutdown Imminent"
                        
                        $SEV_KEY = $OS_EventId + "_tempcritical"
                        $DEFAULT_Severity = 5
                        $DEFAULT_Type = 1
                        $DEFAULT_ExpireTime = 0

                    default:
                }
            }
            else if(int($2) > 1)
            {
                @Summary = "Minor Chassis Alarm"
                
                $SEV_KEY = $OS_EventId + "_chassisminor"
                $DEFAULT_Severity = 3
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
            else if(int($3) > 1)
            {
                @Summary = "Major Chassis Alarm"
                
                $SEV_KEY = $OS_EventId + "_chassismajor"
                $DEFAULT_Severity = 4
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
            else if(match($1, "1")&&match($2, "1")&&match($3, "1"))
            {
                @Summary = "All Chassis Alarms Cleared"
                
                $SEV_KEY = $OS_EventId + "_chassisclear"
                $DEFAULT_Severity = 1
                $DEFAULT_Type = 2
                $DEFAULT_ExpireTime = 0
            }
            else
            {
                @Summary = "Unknown Chassis Alarm Status"
                
                $SEV_KEY = $OS_EventId + "_unknown"
                $DEFAULT_Severity = 2
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap 

        case "6": ### chassisAlarmOff
        
            ##########
            # $1 = chassisTempAlarm - The chassis minor alarm status.
            # $2 = chassisMinorAlarm - The chassis major alarm status.
            # $3 = chassisMajorAlarm - The chassis temperature alarm status.
            ##########

            $chassisTempAlarm = $1
            $chassisMinorAlarm = $2
            $chassisMajorAlarm = $3
            details($chassisTempAlarm,$chassisMinorAlarm,$chassisMajorAlarm)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-chassisAlarmOff"

            @AlertGroup = "Chassis Status"
            @AlertKey = ""
            if(match($1, "1"))
            {
                if(match($2, "1"))
                {
                    if(match($3, "1"))
                    {
                        @Summary = "All Chassis Alarms Cleared"
            
                        $DEFAULT_Severity = 1
                        $DEFAULT_Type = 2
                        $DEFAULT_ExpireTime = 0

                    }
                    else
                    {
                        discard
                    }
                }
                else
                {
                    discard
                }
            }
            else
            {
                discard
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap 

        case "7": ### ipPermitDeniedTrap
        
            ##########
            # $1 = ipPermitDeniedAddress - The IP address of a IP permit denied
            #        access.
            # $2 = ipPermitDeniedAccess - The denied access type.
            ##########

            $ipPermitDeniedAddress = $1
            $ipPermitDeniedAccess = lookup($2, ipPermitDeniedAccess)
            
            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-ipPermitDeniedTrap"

            @AlertGroup = "IP Permit Access"
            @AlertKey = "ipPermitDeniedListEntry." + $1
            @Summary = "Host Denied " + $ipPermitDeniedAccess + " Access  ( Host IP: " + $1 + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2
            
            $ipPermitDeniedAccess = $ipPermitDeniedAccess + " ( " + $2 + " )"
            details($ipPermitDeniedAddress,$ipPermitDeniedAccess)

        case "9": ### sysConfigChangeTrap
        
            ##########
            # $1 = sysConfigChangeTime - The time (in hundredths of a second)
            #        since the configuration of the system was last changed.
            # $2 = sysConfigChangeInfo - Indicates which NVRAM block is changed
            #        by whom.
            ##########
            
            $sysConfigChangeTime = $1
            $sysConfigChangeInfo = $2
            details($sysConfigChangeTime,$sysConfigChangeInfo)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-sysConfigChangeTrap"

            @AlertGroup = "NVRAM Config Change"
            @AlertKey = ""
            @Summary = "NVRAM Config Change, " + $2
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

        case "10": ### tokenRingSoftErrExceededTrap
        
            ##########
            # $1 = ringStationMacAddress 
            # $2 = ifIndex 
            # $3 = ifName
            ##########
        
            $ringStationMacAddress = $1
            $ifIndex = $2
            $ifName = $3
            details($ringStationMacAddress,$ifIndex,$ifName)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STACK-MIB-tokenRingSoftErrExceededTrap"

            @AlertGroup = "Token Ring Soft Error"
            @AlertKey = "ringStationEntry." + extract($OID1, "3\.6\.1\.2\.1\.16\.10\.2\.1\.2\.(.*)$")
            @Summary = "Token Ring Station Exceeded Soft Error Threshold  ( Station: " + $1 + ", Interface: " + $3 + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap 

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-STACK-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-STACK-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-STACK-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-STACK-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-STACK-MIB.include.snmptrap.rules >>>>>")
