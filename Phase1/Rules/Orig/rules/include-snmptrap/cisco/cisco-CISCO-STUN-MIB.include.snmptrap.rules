###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-STUN-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-STUN-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
#     - Added enterprise OID .1.3.6.1.4.1.9.9.30.
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.30.2"|".1.3.6.1.4.1.9.9.30": ### Cisco Serial Tunneling (STUN) - Notifications from CISCO-STUN-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-STUN-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Serial Tunneling"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### stunPeerStateChangeNotification

            ##########
            # This notification indicates that the state of a STUN route has
            # transitioned to active (connected or direct) or inactive (dead
            # or closed).
            #
            # $1 = stunRoutePeerState - The state of the peer connection
            #        through the STUN tunnel.
            ##########

            $stunRoutePeerState = lookup($1, stunRoutePeerState) + " ( " + $1 + " )"
            $stunGroupIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $stunRouteStationAddress = extract($OID1, "\.([0-9]+)$")
            details($stunRoutePeerState,$stunGroupIndex,$stunRouteStationAddress)
            
            $OS_EventId = "SNMPTRAP-cisco-CISCO-STUN-MIB-stunPeerStateChangeNotification"
            
            @AlertGroup = "STUN Peer State Change"
            @AlertKey = "stunRouteEntry." + $stunGroupIndex + "." + $stunRouteStationAddress
            switch($1)
            {
                case "1": ### dead
                    @Summary = "STUN Route Dead"
                    
                    $SEV_KEY = $OS_EventId + "_dead"
                    $DEFAULT_Severity = 4
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### closed
                    @Summary = "STUN Route Closed"
                    
                    $SEV_KEY = $OS_EventId + "_closed"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### opening
                    @Summary = "STUN Route Opening"
                    
                    $SEV_KEY = $OS_EventId + "_opening"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### openWait
                    @Summary = "STUN Route Open, Waiting"
                    
                    $SEV_KEY = $OS_EventId + "_openWait"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "5": ### connected
                    @Summary = "STUN Route Connected"
                    
                    $SEV_KEY = $OS_EventId + "_connected"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "6": ### direct
                    @Summary = "STUN Route Direct"
                    
                    $SEV_KEY = $OS_EventId + "_direct"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "STUN Route Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-STUN-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-STUN-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-STUN-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-STUN-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-STUN-MIB.include.snmptrap.rules >>>>>")
