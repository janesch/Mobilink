##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-OPTICAL-PATCH-MIB.include.snmptrap.rules 2.1
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-OPTICAL-PATCH-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.10.67.2": ### Cisco Optical Patch - Notifications from CISCO-OPTICAL-PATCH-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-OPTICAL-PATCH-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Optical Patch"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### cOPatchEvent

            ##########
            # $1 = cOPatchLowIfIndex 
            # $2 = cOPatchHighIfIndex
            # $3 = cOPatchType 
            # $4 = cOPatchStatus 
            # $5 = cOPatchEventType
            ##########

            $cOPatchLowIfIndex = $1
            $cOPatchHighIfIndex = $2
            $cOPatchType = lookup($3, cOPatchType)
            $cOPatchStatus = lookup($4, cOPatchStatus) + " ( " + $4 + " )"
            $cOPatchEventType = lookup($5, cOPatchEventType)
            $cOPatchIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-OPTICAL-PATCH-MIB-cOPatchEvent"

            @AlertGroup = "Optical Patch Status"
            @AlertKey = "cOPatchEntry." + $cOPatchIndex 
            switch($4)
            {
                case "1": ### noError
                    @Summary = $cOPatchType + " Optical Patch " + $cOPatchEventType + ", Normal"
                    
                    $SEV_KEY = $OS_EventId + "_noError"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### otherError
                    @Summary = $cOPatchType + " Optical Patch " + $cOPatchEventType + ", Unknown Error"
                    
                    $SEV_KEY = $OS_EventId + "_otherError"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### interfaceError
                    @Summary = $cOPatchType + " Optical Patch " + $cOPatchEventType + ", Wrong Interface Type"
                    
                    $SEV_KEY = $OS_EventId + "_interfaceError"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### interfaceChannelError
                    @Summary = $cOPatchType + " Optical Patch " + $cOPatchEventType + ", Frequency Channel Mismatch"
                    
                    $SEV_KEY = $OS_EventId + "_interfaceChannelError"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = $cOPatchType + " Optical Patch " + $cOPatchEventType + ", Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( Patch: " + @AlertKey + ", Low: ifEntry." + $1 + ", High: ifEntry." + $2 + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $4 + " " + $5
            
            $cOPatchType = $cOPatchType + " ( " + $3 + " )"
            $cOPatchEventType = $cOPatchEventType + " ( " + $5 + " )"
            details($cOPatchLowIfIndex,$cOPatchHighIfIndex,$cOPatchType,$cOPatchStatus,$cOPatchEventType,$cOPatchIndex)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-OPTICAL-PATCH-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-OPTICAL-PATCH-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-OPTICAL-PATCH-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-OPTICAL-PATCH-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-OPTICAL-PATCH-MIB.include.snmptrap.rules >>>>>")
