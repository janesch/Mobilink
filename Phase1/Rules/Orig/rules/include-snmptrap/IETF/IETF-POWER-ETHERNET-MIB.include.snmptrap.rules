###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    IETF-POWER-ETHERNET-MIB.include.snmptrap.rules 1.0
#    2006/05/16 Rajyalakshmi Reddy
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  POWER-ETHERNET-MIB
#
###############################################################################

case ".1.3.6.1.2.1.105": ### Power Source Equipment (PSE) - Notifications from POWER-ETHERNET-MIB (20031124)

    log(DEBUG, "<<<<< Entering... IETF-POWER-ETHERNET-MIB.include.snmptrap.rules >>>>>")

    @Agent = "IETF-POWER-ETHERNET-MIB"
    @Class = "40086"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### pethPsePortOnOffNotification

            ##########
            # $1 = pethPsePortDetectionStatus 
            ##########

            $pethPsePortDetectionStatus = lookup($1, pethPsePortDetectionStatus) + " ( " + $1 + " )"
            $pethPsePortGroupIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $pethPsePortIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-POWER-ETHERNET-MIB-pethPsePortOnOffNotification"

            @AlertGroup = "PSE Port Status"
            @AlertKey = "pethPsePortEntry." + $pethPsePortGroupIndex + "." + $pethPsePortIndex
            switch($1)
            {
                case "1": ### disabled
                    $SEV_KEY = $OS_EventId + "_disabled"
                    @Summary = "Power Source Equipment Port Disabled"

                    $DEFAULT_Severity = 3 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "2": ### searching 
                    $SEV_KEY = $OS_EventId + "_searching"
                    @Summary = "Power Source Equipment Port in Searching"

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "3": ### deliveringPower 
                    $SEV_KEY = $OS_EventId + "_deliveringPower"
                    @Summary = "Power Source Equipment Port Delivering Power"

                    $DEFAULT_Severity = 1 
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "4": ### fault 
                    $SEV_KEY = $OS_EventId + "_fault"
                    @Summary = "Power Source Equipment Port Fault, TEST_ERROR"

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "5": ### test 
                    $SEV_KEY = $OS_EventId + "_test"
                    @Summary = "Power Source Equipment Port in TEST_MODE"

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                case "6": ### otherFault 
                    $SEV_KEY = $OS_EventId + "_otherFault"
                    @Summary = "Power Source Equipment Port Idle, Variable Error_Conditions"

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                default: ### unknown 
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "Power Source Equipment Port Status Unknown" 

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }
            @Summary = @Summary + "  ( Group: " + $pethPsePortGroupIndex + ", Port: " + $pethPsePortIndex + " )"

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

            details($pethPsePortDetectionStatus,$pethPsePortGroupIndex,$pethPsePortIndex)

        case "2": ### pethMainPowerUsageOnNotification

            ##########
            # $1 = pethMainPseConsumptionPower 
            ##########

            $pethMainPseConsumptionPower = $1
            $pethMainPseGroupIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-POWER-ETHERNET-MIB-pethMainPowerUsageOnNotification"

            @AlertGroup = "PSE Power Usage Status"
            @AlertKey = "pethMainPseEntry." + $pethMainPseGroupIndex
            @Summary = "PSE Power Usage, " + $1 + " Watts, Above Threshold  ( Group: " + $pethMainPseGroupIndex + " )"

            $DEFAULT_Severity = 2 
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($pethMainPseConsumptionPower,$pethMainPseGroupIndex)

        case "3": ### pethMainPowerUsageOffNotification

            ##########
            # $1 = pethMainPseConsumptionPower 
            ##########

                $pethMainPseConsumptionPower = $1
            $pethMainPseGroupIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-POWER-ETHERNET-MIB-pethMainPowerUsageOffNotification"

            @AlertGroup = "PSE Power Usage Status"
            @AlertKey = "pethMainPseEntry." + $pethMainPseGroupIndex
            @Summary = "PSE Power Usage, " + $1 + " Watts, Below Threshold  ( Group: " + $pethMainPseGroupIndex + " )"

            $DEFAULT_Severity = 1 
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($pethMainPseConsumptionPower,$pethMainPseGroupIndex)


        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, IETF-POWER-ETHERNET-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-POWER-ETHERNET-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-POWER-ETHERNET-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-POWER-ETHERNET-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... IETF-POWER-ETHERNET-MIB.include.snmptrap.rules >>>>>")
