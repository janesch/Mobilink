###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    IETF-DIAL-CONTROL-MIB.include.snmptrap.rules 3.1
#    2005/03/06 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  IETF-DIAL-CONTROL-MIB
#
###############################################################################

case ".1.3.6.1.2.1.10.21.2": ### Dial Control - Notifications from DIAL-CONTROL-MIB (RFC2128)

    log(DEBUG, "<<<<< Entering... IETF-DIAL-CONTROL-MIB.include.snmptrap.rules >>>>>")

    @Agent = "IETF-DIAL-CONTROL-MIB"
    @Class = "40086"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### dialCtlPeerCallInformation
        
            ##########
            # $1 = callHistoryPeerId
            # $2 = callHistoryPeerIfIndex
            # $3 = callHistoryLogicalIfIndex
            # $4 = ifOperStatus
            # $5 = callHistoryPeerAddress
            # $6 = callHistoryPeerSubAddress
            # $7 = callHistoryDisconnectCause
            # $8 = callHistoryConnectTime
            # $9 = callHistoryDisconnectTime
            # $10 = callHistoryInfoType
            # $11 = callHistoryCallOrigin
            ##########

            $callHistoryPeerId = $1
            $callHistoryPeerIfIndex = $2
            $callHistoryLogicalIfIndex = $3
            $ifOperStatus = lookup($4, ifOperStatus) + " ( " + $4 + " )"
            $callHistoryPeerAddress = $5
            $callHistoryPeerSubAddress = $6
            $callHistoryDisconnectCause = rtrim($7)
            $callHistoryConnectTime = $8
            $callHistoryDisconnectTime = $9
            $callHistoryInfoType = lookup($10, callInfoType)
            $callHistoryCallOrigin = lookup($11, callCallOrigin)
                        
            $OS_EventId = "SNMPTRAP-IETF-DIAL-CONTROL-MIB-dialCtlPeerCallInformation"
                
            @AlertGroup = "Peer Call Status"
            @AlertKey = "dialCtlPeerCfgEntry." + $1 + "." + $2
            switch($11)
            {
                case "1"|"2": ### originate and answer
                    @Summary = $callHistoryCallOrigin + " " + $callHistoryInfoType + " Call Cleared or Failed, Disconnect Cause: " + $callHistoryDisconnectCause
                case "3": ### callback
                    @Summary = $callHistoryInfoType + " Callback Cleared or Failed, Disconnect Cause: " + $callHistoryDisconnectCause
                default:
                    @Summary = $callHistoryInfoType + " Call Cleared or Failed, Disconnect Cause: " + $callHistoryDisconnectCause
            }
            if(match($5, ""))
            {
                @Summary = @Summary + "  ( Peer ID: " + $1 + ", Peer ifIndex: " + $2 + ", Logical ifIndex: " + $3 + " )"
            }
            else
            {
                @Summary = @Summary + "  ( Peer Address: " + $5
                if(match($6, ""))
                {
                    @Summary = @Summary + " )"
                }
                else
                {
                    @Summary = @Summary + ", Peer Sub-Address: " + $6 + " )"
                }
            }
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
            $callHistoryInfoType = $callHistoryInfoType + " ( " + $10 + " )"
            $callHistoryCallOrigin = $callHistoryCallOrigin + " ( " + $11 + " )"
            details($callHistoryPeerId,$callHistoryPeerIfIndex,$callHistoryLogicalIfIndex,$ifOperStatus,$callHistoryPeerAddress,$callHistoryPeerSubAddress,$callHistoryDisconnectCause,$callHistoryConnectTime,$callHistoryDisconnectTime,$callHistoryInfoType,$callHistoryCallOrigin)

        case "2": ### dialCtlPeerCallSetup
        
            ##########
            # $1 = callActivePeerId
            # $2 = callActivePeerIfIndex
            # $3 = callActiveLogicalIfIndex
            # $4 = ifOperStatus
            # $5 = callActivePeerAddress
            # $6 = callActivePeerSubAddress
            # $7 = callActiveInfoType
            # $8 = callActiveCallOrigin
            ##########

            $callActivePeerId = $1
            $callActivePeerIfIndex = $2
            $callActiveLogicalIfIndex = $3
            $ifOperStatus = lookup($4, ifOperStatus) + " ( " + $4 + " )"
            $callActivePeerAddress = $5
            $callActivePeerSubAddress = $6
            $callActiveInfoType = lookup($7, callInfoType)
            $callActiveCallOrigin = lookup($8, callCallOrigin)
                        
            $OS_EventId = "SNMPTRAP-IETF-DIAL-CONTROL-MIB-dialCtlPeerCallSetup"
                
            @AlertGroup = "Peer Call Status"
            @AlertKey = "dialCtlPeerCfgEntry." + $1 + "." + $2
            switch($8)
            {
                case "1"|"2": ### originate and answer
                    @Summary = $callActiveCallOrigin + " " + $callActiveInfoType + " Call Successful"
                case "3": ### callback
                    @Summary = $callActiveInfoType + " Callback Successful"
                default:
                    @Summary = $callActiveInfoType + " Call Successful"
            }
            if(match($5, ""))
            {
                @Summary = @Summary + "  ( Peer ID: " + $1 + ", Peer ifIndex: " + $2 + ", Logical ifIndex: " + $3 + " )"
            }
            else
            {
                @Summary = @Summary + "  ( Peer Address: " + $5
                if(match($6, ""))
                {
                    @Summary = @Summary + " )"
                }
                else
                {
                    @Summary = @Summary + ", Peer Sub-Address: " + $6 + " )"
                }
            }
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            $callActiveInfoType = $callActiveInfoType + " ( " + $7 + " )"
            $callActiveCallOrigin = $callActiveCallOrigin + " ( " + $8 + " )"
            details($callActivePeerId,$callActivePeerIfIndex,$callActiveLogicalIfIndex,$ifOperStatus,$callActivePeerAddress,$callActivePeerSubAddress,$callActiveInfoType,$callActiveCallOrigin)

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, IETF-DIAL-CONTROL-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-DIAL-CONTROL-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-DIAL-CONTROL-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-DIAL-CONTROL-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... IETF-DIAL-CONTROL-MIB.include.snmptrap.rules >>>>>")
