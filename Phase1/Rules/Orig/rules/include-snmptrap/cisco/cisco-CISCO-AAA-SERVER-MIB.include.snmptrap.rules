###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-AAA-SERVER-MIB.include.snmptrap.rules 2.1 
#    2005/03/03 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-AAA-SERVER-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.10.56.2": ### Cisco AAA Server - Notifications from CISCO-AAA-SERVER-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-AAA-SERVER-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-AAA Server"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"
    
    switch($specific-trap)
    {
        case "1": ### casServerStateChange
        
            ##########
            # $1 = casState 
            # $2 = casPreviousStateDuration 
            # $3 = casTotalDeadTime 
            ##########

            $casState = lookup($1, ciscoCasState) + " ( " + $1 + " )"
            $casPreviousStateDuration = $2
            $casTotalDeadTime = $3
            $casProtocol_Raw = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $casProtocol = lookup($casProtocol_Raw, CiscoAAAProtocol) + " ( " + $casProtocol_Raw + " )"
		  $casIndex = extract($OID1, "\.([0-9]+)$")
            details($casState,$casPreviousStateDuration,$casTotalDeadTime,$casProtocol,$casIndex)
            
            $OS_EventId = "SNMPTRAP-cisco-CISCO-AAA-SERVER-MIB-casServerStateChange"

            @AlertGroup = "AAA Server Status"
            @AlertKey = "casConfigEntry." + $casProtocol_Raw + "." + $casIndex
            switch($1)
            {
                case "1": ### up
                    @Summary = "AAA Server Up  ( " + @AlertKey + " )"
                    
                    $SEV_KEY = $OS_EventId + "_up"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### dead
                    @Summary = "AAA Server Down  ( " + @AlertKey + " )"
                    
                    $SEV_KEY = $OS_EventId + "_dead"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "AAA Server Status Unknown  ( " + @AlertKey + " )"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-AAA-SERVER-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-AAA-SERVER-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-AAA-SERVER-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-AAA-SERVER-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-AAA-SERVER-MIB.include.snmptrap.rules >>>>>")
