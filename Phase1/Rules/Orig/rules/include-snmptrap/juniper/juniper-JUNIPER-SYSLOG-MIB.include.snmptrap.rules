###############################################################################
#
#    Licensed Materials - Property of IBM
#    5724-048
#
#    (C) Copyright IBM Corp. 2008
#    All Rights Reserved
#
#    US Government Users Restricted Rights - Use, duplication or
#    disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
#
#    juniper-JUNIPER-SYSLOG-MIB.include.snmptrap.rules 6.0
#    2008/04/08 Anchala Bansal
#
###############################################################################
#
# 6.0 - Updated release for JUNOS 9.0
#
#          - Repackaged for NIM-06
#
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/OMNIbus 3.6 and 7.x
#          -  Netcool Rules File Standards (MUSE-STD-RF-03, Jan 2006)
#          -  JUNIPER-SYSLOG-MIB
#
###############################################################################

case ".1.3.6.1.4.1.2636.4.12": ### Juniper Syslogs by JUNOS - Notifications from JUNIPER-SYSLOG-MIB (20060320)

    log(DEBUG, "<<<<< Entering... juniper-JUNIPER-SYSLOG-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Juniper-Syslog"
    @Class = "40200"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### jnxSyslogTrap

            ##########
            # $1 = jnxSyslogEventName 
            # $2 = jnxSyslogTimestamp 
            # $3 = jnxSyslogSeverity 
            # $4 = jnxSyslogFacility 
            # $5 = jnxSyslogProcessId 
            # $6 = jnxSyslogProcessName 
            # $7 = jnxSyslogHostName 
            # $8 = jnxSyslogMessage 
            ##########

            $jnxSyslogEventName = $1
            $jnxSyslogTimestamp = $2
            $jnxSyslogSeverity = lookup($3, JnxSyslogSeverity) + " ( " + $3 + " )"
            $jnxSyslogFacility = lookup($4, JnxSyslogFacility)
            $jnxSyslogProcessId = $5
            $jnxSyslogProcessName = $6
            $jnxSyslogHostName = $7
            $jnxSyslogMessage = $8
	    $jnxSyslogAvAttribute = $9
	    $jnxSyslogAvValue = $10
	    if(exists($9) && exists($10))
	    {
	    	$jnxSyslogId = extract($OID8, "\.3\.6\.1\.4\.1\.2636\.3\.35\.1\.1\.1\.9\.([0-9]+)\.[0-9]+$")
	    	$jnxSyslogAvIndex = extract($OID8, "\.3\.6\.1\.4\.1\.2636\.3\.35\.1\.1\.1\.9\.[0-9]+\.([0-9]+)$")
	    }
	    else
	    {
	    	$jnxSyslogId = extract($OID8, "\.3\.6\.1\.4\.1\.2636\.3\.35\.1\.1\.1\.9\.([0-9]+)$")
	    }

            $OS_EventId = "SNMPTRAP-juniper-JUNIPER-SYSLOG-MIB-jnxSyslogTrap"

            @AlertGroup = "JUNOS-" + $jnxSyslogFacility

	    if(exists($9) && exists($10))
	    {
            	@AlertKey = "jnxSyslogAvEntry." + $jnxSyslogId + "." + $jnxSyslogAvIndex
	    }
	    else
	    {
            	@AlertKey = "jnxSyslogEntry." + $jnxSyslogId
	    }

            @Summary = $8 + "  ( " + @AlertKey + " )"

	    switch($3)
	    {
		case "1": ### emergency
		    $SEV_KEY = $OS_EventId + "_emergency"

	            $DEFAULT_Severity = 5
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
		case "2": ### alert
		    $SEV_KEY = $OS_EventId + "_alert"

	            $DEFAULT_Severity = 5
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
		case "3": ### critical
		    $SEV_KEY = $OS_EventId + "_critical"

	            $DEFAULT_Severity = 4
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
		case "4": ### error
		    $SEV_KEY = $OS_EventId + "_error"

	            $DEFAULT_Severity = 3
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
		case "5": ### warning
		    $SEV_KEY = $OS_EventId + "_warning"

	            $DEFAULT_Severity = 2
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
		case "6": ### notice
		    $SEV_KEY = $OS_EventId + "_notice"

	            $DEFAULT_Severity = 1
	            $DEFAULT_Type = 2
	            $DEFAULT_ExpireTime = 0
		case "7": ### info
		    $SEV_KEY = $OS_EventId + "_info"

	            $DEFAULT_Severity = 1
	            $DEFAULT_Type = 2
	            $DEFAULT_ExpireTime = 0
		case "8": ### debug
		    $SEV_KEY = $OS_EventId + "_debug"

	            $DEFAULT_Severity = 2
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
		default: ### unknown
		    $SEV_KEY = $OS_EventId + "_unknown"

	            $DEFAULT_Severity = 2
	            $DEFAULT_Type = 1
	            $DEFAULT_ExpireTime = 0
	    }

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

	    $jnxSyslogFacility = $jnxSyslogFacility + " ( " + $4 + " )"

	    if(exists($9) && exists($10))
	    {
            	details($jnxSyslogEventName,$jnxSyslogTimestamp,$jnxSyslogSeverity,$jnxSyslogFacility,$jnxSyslogProcessId,$jnxSyslogProcessName,$jnxSyslogHostName,$jnxSyslogMessage,$jnxSyslogAvAttribute,$jnxSyslogAvValue,$jnxSyslogId,$jnxSyslogAvIndex)
	    }
	    else
	    {
            	details($jnxSyslogEventName,$jnxSyslogTimestamp,$jnxSyslogSeverity,$jnxSyslogFacility,$jnxSyslogProcessId,$jnxSyslogProcessName,$jnxSyslogHostName,$jnxSyslogMessage,$jnxSyslogId)
	    }

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, juniper-JUNIPER-SYSLOG-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, juniper-JUNIPER-SYSLOG-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/juniper/juniper-JUNIPER-SYSLOG-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/juniper/juniper-JUNIPER-SYSLOG-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... juniper-JUNIPER-SYSLOG-MIB.include.snmptrap.rules >>>>>")
