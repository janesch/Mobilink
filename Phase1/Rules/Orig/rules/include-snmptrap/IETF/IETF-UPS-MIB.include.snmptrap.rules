###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    IETF-UPS-MIB.include.snmptrap.rules 2.1
#    2005/03/07 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  IETF-UPS-MIB
#
###############################################################################

case ".1.3.6.1.2.1.33.2": ### Uninterruptible Power Supplies - Notification from UPS-MIB (RFC1628)

    log(DEBUG, "<<<<< Entering... IETF-UPS-MIB.include.snmptrap.rules >>>>>")

    @Agent = "IETF-UPS-MIB"
    @Class = "40086"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### upsTrapOnBattery

            ##########
            # $1 = upsEstimatedMinutesRemaining
            # $2 = upsSecondsOnBattery
            # $3 = upsConfigLowBattTime
            ##########

            $upsEstimatedMinutesRemaining = $1
            $upsSecondsOnBattery = $2
            $upsConfigLowBattTime = $3
            details($upsEstimatedMinutesRemaining,$upsSecondsOnBattery,$upsConfigLowBattTime)
            
            $OS_EventId = "SNMPTRAP-IETF-UPS-MIB-upsTrapOnBattery"
            
            @AlertGroup = "UPS Status"
            @AlertKey = ""
            @Summary = "UPS Operating on Battery Power, ~" + $1 + " Minutes Remaining"
            
            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "2": ### upsTrapTestCompleted

            ##########
            # $1 = upsTestId
            # $2 = upsTestSpinLock
            # $3 = upsTestResultsSummary
            # $4 = upsTestResultsDetail
            # $5 = upsTestStartTime
            # $6 = upsTestElapsedTime
            ##########

            $upsTestId = $1
            $upsTestSpinLock = $2
            $upsTestResultsSummary = lookup($3, upsTestResultsSummary)
            $upsTestResultsDetail = $4
            $upsTestStartTime = $5
            $upsTestElapsedTime = $6
            
            $OS_EventId = "SNMPTRAP-IETF-UPS-MIB-upsTrapTestCompleted"
            
            @AlertGroup = "UPS Test Status"
            @AlertKey = "Test ID: " + $1
            switch($3)
            {
                case "1": ### donePass
                    @Summary = "UPS Test " + $upsTestResultsSummary + ", " + $4
                    
                    $SEV_KEY = $OS_EventId + "_donePass"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0

                case "2": ### doneWarning
                    @Summary = "UPS Test " + $upsTestResultsSummary + ", " + $4
                    
                    $SEV_KEY = $OS_EventId + "_doneWarning"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

                case "3": ### doneError
                    @Summary = "UPS Test " + $upsTestResultsSummary + ", " + $4
                    
                    $SEV_KEY = $OS_EventId + "_doneError"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

                case "4": ### aborted
                    @Summary = "UPS Test " + $upsTestResultsSummary + ", " + $4
                    
                    $SEV_KEY = $OS_EventId + "_aborted"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

                case "5": ### inProgress
                    @Summary = "UPS Test " + $upsTestResultsSummary + ", " + $4
                    
                    $SEV_KEY = $OS_EventId + "_inProgress"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

                case "6": ### noTestsInitiated
                    @Summary = "UPS Test " + $upsTestResultsSummary + ", " + $4
                    
                    $SEV_KEY = $OS_EventId + "_noTestsInitiated"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

                default:
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3 + " " + $4
            
            $upsTestResultsSummary = $upsTestResultsSummary + " ( " + $3 + " )"
            details($upsTestId,$upsTestSpinLock,$upsTestResultsSummary,$upsTestResultsDetail,$upsTestStartTime,$upsTestElapsedTime)

        case "3": ### upsTrapAlarmEntryAdded

            ##########
            # $1 = upsAlarmId
            # $2 = upsAlarmDescr
            ##########

            $upsAlarmId = $1
            $upsAlarmDescr = $2
            details($upsAlarmId,$upsAlarmDescr)
            
            [@AlertGroup,$OS_X733EventType,$OS_X733ProbableCause,$OS_X733SpecificProb,@Summary] = lookup(extract($2, "\.(33\.1\.6\.3\.[0-9]+)$"), upsWellKnownAlarms)
            
            $OS_EventId = "SNMPTRAP-IETF-UPS-MIB-upsTrapAlarmEntryAdded"
            
            @AlertKey = "upsAlarmEntry." + $1
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

        case "4": ### upsTrapAlarmEntryRemoved

            ##########
            # $1 = upsAlarmId
            # $2 = upsAlarmDescr
            ##########

            $upsAlarmId = $1
            $upsAlarmDescr = $2
            details($upsAlarmId,$upsAlarmDescr)
            
            [@AlertGroup,$OS_X733EventType,$OS_X733ProbableCause,$OS_X733SpecificProb,@Summary] = lookup(extract($2, "\.(33\.1\.6\.3\.[0-9]+)$"), upsWellKnownAlarms)
            
            $OS_EventId = "SNMPTRAP-IETF-UPS-MIB-upsTrapAlarmEntryRemoved"
            
            @AlertKey = "upsAlarmEntry." + $1
            @Summary = "End of " + @Summary
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, IETF-UPS-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-UPS-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-UPS-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-UPS-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... IETF-UPS-MIB.include.snmptrap.rules >>>>>")
