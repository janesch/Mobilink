###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-ALPS-MIB.include.snmptrap.rules 2.1 
#    2005/03/03 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-ALPS-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.95.2": ### Cisco Airline Protocol Support - ALPS Notifications from CISCO-ALPS-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-ALPS-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Airline Protocol"
    @Class = "40057"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### alpsPeerStatusChange

            ##########
            # $1 = alpsRemPeerState 
            ##########

            $alpsRemPeerState = lookup($1, alpsRemPeerState) + " ( " + $1 + " )"
            $alpsRemPeerIpAddr = extract($OID1, "\.([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)$")
            details($alpsRemPeerState,$alpsRemPeerIpAddr)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ALPS-MIB-alpsPeerStatusChange"

            @AlertGroup = "ALPS Peer Connection Status"
            @AlertKey = "alpsRemPeerEntry." + $alpsRemPeerIpAddr
            switch($1)
            {
                case "1": ### closed
                    @Summary = "ALPS Peer Connection Closed"
                    
                    $SEV_KEY = $OS_EventId + "_closed"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### opening
                    @Summary = "ALPS Peer Connection Opening"
                    
                    $SEV_KEY = $OS_EventId + "_opening"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### opened
                    @Summary = "ALPS Peer Connection Opened"
                    
                    $SEV_KEY = $OS_EventId + "_opened"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "ALPS Peer Connection Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( Peer: " + $alpsRemPeerIpAddr + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        case "2": ### alpsCktStatusChange

            ##########
            # $1 = alpsCktBaseState
            ##########

            $alpsCktBaseState = $1
            
            $OctetString = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.1\.1\.6\.(.*)\.[0-9]+$")
            include "$NC_RULES_HOME/include-snmptrap/decodeOctetString.include.snmptrap.rules"
            $alpsCktBaseName = $String
            
            $alpsCktBaseDlcType = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ALPS-MIB-alpsCktStatusChange"

            @AlertGroup = "ALPS Circuit Status"
            @AlertKey = "alpsCktBaseEntry." + extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.1\.1\.6\.(.*)$")
            
            switch($alpsCktBaseDlcType)
            { 
                case "1": ### emtox
                    @Summary = "ALPS EMTOX Circuit"
                case "2": ### ax25
                    @Summary = "ALPS AX25 Circuit"
                case "3": ### alc
                    @Summary = "ALPS ALC Circuit"
                case "4": ### uts
                    @Summary = "ALPS UTS Circuit"
                default:
                    @Summary = "ALPS Circuit"
            }
            
            switch ($1)
            {
                case "1": ### disabled
                    @Summary = @Summary + " Disabled"
                    
                    $SEV_KEY = $OS_EventId + "_disabled"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### inoperable
                    @Summary = @Summary + " Inoperable"
                    
                    $SEV_KEY = $OS_EventId + "_inoperable"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### opening
                    @Summary = @Summary + " Opening"
                    
                    $SEV_KEY = $OS_EventId + "_opening"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### opened
                    @Summary = @Summary + " Opened"
                    
                    $SEV_KEY = $OS_EventId + "_opened"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "5": ### cktBusy
                    @Summary = @Summary + " Busy"
                    
                    $SEV_KEY = $OS_EventId + "_cktBusy"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "6": ### peerBusy
                    @Summary = @Summary + " Peer Busy"
                    
                    $SEV_KEY = $OS_EventId + "_peerBusy"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "7": ### updating
                    @Summary = @Summary + " Updating"
                    
                    $SEV_KEY = $OS_EventId + "_updating"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = @Summary + " Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( Circuit: " + $alpsCktBaseName + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $alpsCktBaseDlcType = lookup($alpsCktBaseDlcType, alpsCktBaseDlcType) + " ( " + $alpsCktBaseDlcType + " )"
            details($alpsCktBaseState,$alpsCktBaseName,$alpsCktBaseDlcType)

        case "3": ### alpsAscuStatusChange

            ##########
            # $1 = alpsAscuState 
            ##########

            $alpsAscuState = lookup($1, alpsAscuState) + " ( " + $1 + " )"
            $ifIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $alpsAscuId = extract($OID1, "\.([0-9]+)$")
            details($alpsAscuState,$ifIndex,$alpsAscuId)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ALPS-MIB-alpsAscuStatusChange"

            @AlertGroup = "ALPS ASCU Status"
            @AlertKey = "alpsAscuEntry." + $ifIndex + "." + $alpsAscuId
            switch($1)
            {
                case "1": ### disabled
                    @Summary = "ALPS ASCU Disabled"
                    
                    $SEV_KEY = $OS_EventId + "_disabled"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### unknown
                    @Summary = "ALPS ASCU Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### down
                    @Summary = "ALPS ASCU Down"
                    
                    $SEV_KEY = $OS_EventId + "_down"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### up
                    @Summary = "ALPS ASCU Up"
                    
                    $SEV_KEY = $OS_EventId + "_up"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "ALPS ASCU Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        case "4": ### alpsPeerConnStatusChange

            ##########
            # $1 = alpsRemPeerConnState 
            ##########

            $alpsRemPeerConnState = lookup($1, alpsRemPeerConnState) + " ( " + $1 + " )"
            $alpsRemPeerConnIpAddr = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.2\.4\.1\.5\.([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)\..*$")
            
            $OctetString = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.2\.4\.1\.5\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.(.*)$")
            include "$NC_RULES_HOME/include-snmptrap/decodeOctetString.include.snmptrap.rules"
            $alpsRemPeerConnIdString = $String
            
            details($alpsRemPeerConnState,$alpsRemPeerConnIpAddr,$alpsRemPeerConnIdString)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ALPS-MIB-alpsPeerConnStatusChange"

            @AlertGroup = "ALPS Peer Connection Status"
            @AlertKey = "alpsRemPeerConnEntry." + extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.2\.4\.1\.5\.(.*)$") ### alpsRemPeerConnIpAddr.alpsRemPeerConnIdString
            switch($1)
            {
                case "1": ### closed
                    @Summary = "ALPS Peer Connection Closed"
                    
                    $SEV_KEY = $OS_EventId + "_closed"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### opening
                    @Summary = "ALPS Peer Connection Opening"
                    
                    $SEV_KEY = $OS_EventId + "_opening"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### opened
                    @Summary = "ALPS Peer Connection Opened"
                    
                    $SEV_KEY = $OS_EventId + "_opened"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### busy
                    @Summary = "ALPS Peer Connection Busy"
                    
                    $SEV_KEY = $OS_EventId + "_busy"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "ALPS Peer Connection Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( Peer: " + $alpsRemPeerConnIpAddr + ", Connection: " + $alpsRemPeerConnIdString + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        case "5": ### alpsCktOpenFailure

            ##########
            # $1 = alpsCktBaseDownReason 
            ##########

            $alpsCktBaseDownReason = lookup($1, alpsCktBaseDownReason)
            
            $OctetString = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.1\.1\.14\.(.*)\.[0-9]+$")
            include "$NC_RULES_HOME/include-snmptrap/decodeOctetString.include.snmptrap.rules"
            $alpsCktBaseName = $String
            
            $alpsCktBaseDlcType = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ALPS-MIB-alpsCktOpenFailure"

            @AlertGroup = "ALPS Circuit Status"
            @AlertKey = "alpsCktBaseEntry." + extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.1\.1\.14\.(.*)$")
            
            switch($alpsCktBaseDlcType)
            { 
                case "1": ### emtox
                    @Summary = "ALPS EMTOX Circuit"
                case "2": ### ax25
                    @Summary = "ALPS AX25 Circuit"
                case "3": ### alc
                    @Summary = "ALPS ALC Circuit"
                case "4": ### uts
                    @Summary = "ALPS UTS Circuit"
                default:
                    @Summary = "ALPS Circuit"
            }
            
            @Summary = @Summary + " Open Failure, " + $alpsCktBaseDownReason + "  ( Circuit: " + $alpsCktBaseName + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $alpsCktBaseDownReason = $alpsCktBaseDownReason + " ( " + $1 + " )"
            details($alpsCktBaseDownReason)

        case "6": ### alpsCktPartialReject

            ##########
            # $1 = alpsCktAscuIfIndex 
            # $2 = alpsCktAscuId 
            ##########

            $alpsCktAscuIfIndex = $1
            $alpsCktAscuId = $2
            
            $OctetString = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.4\.1\.5\.(.*)\.[0-9]+\.[0-9]+\.[0-9]+$")
            include "$NC_RULES_HOME/include-snmptrap/decodeOctetString.include.snmptrap.rules"
            $alpsCktAscuCktName = $String
            
            $alpsCktAscuCktDlcType = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.4\.1\.5\..*\.([0-9]+)\.[0-9]+\.[0-9]+$")
            $alpsCktAscuA1 = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.4\.1\.5\..*\.[0-9]+\.([0-9]+)\.[0-9]+$")
            $alpsCktAscuA2 = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.4\.1\.5\..*\.[0-9]+\.[0-9]+\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ALPS-MIB-alpsCktPartialReject"

            @AlertGroup = "ALPS Circuit ASCU Status"
            @AlertKey = "alpsCktAscuEntry." + extract($OID1, "3\.6\.1\.4\.1\.9\.9\.95\.1\.3\.4\.1\.5\.(.*)$")
            switch($alpsCktAscuCktDlcType)
            { 
                case "1": ### emtox
                    @Summary = "ALPS EMTOX Circuit Rejected ASCU"
                case "2": ### ax25
                    @Summary = "ALPS AX25 Circuit Rejected ASCU"
                case "3": ### alc
                    @Summary = "ALPS ALC Circuit Rejected ASCU"
                case "4": ### uts
                    @Summary = "ALPS UTS Circuit Rejected ASCU"
                default:
                    @Summary = "ALPS Circuit Rejected ASCU"
            }
            @Summary = @Summary + "  ( Circuit: " + $alpsCktAscuCktName + ", ASCU: alpsAscuEntry." + $1 + "." + $2 + " ) "
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
            $alpsCktAscuCktDlcType = lookup($alpsCktAscuCktDlcType, alpsCktAscuCktDlcType) + " ( " + $alpsCktAscuCktDlcType + " )"
            details($alpsCktAscuIfIndex,$alpsCktAscuId,$alpsCktAscuCktDlcType,$alpsCktAscuA1,$alpsCktAscuA2)

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-ALPS-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-ALPS-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ALPS-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ALPS-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-ALPS-MIB.include.snmptrap.rules >>>>>")
