##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-ENTITY-ALARM-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-ENTITY-ALARM-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.138.2": ### Cisco Physical Entity Alarm Monitoring - Notifications from CISCO-ENTITY-ALARM-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-ENTITY-ALARM-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Entity Alarm"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    ##########
    # NOTE: All Cisco Physical Entity Alarm Monitoring traps contain the
    # following variable bindings:
    #
    # $1 = ceAlarmHistEntPhysicalIndex - This object specifies the physical
    #        entity that generated the alarm.
    # $2 = ceAlarmHistAlarmType - This object specifies the type of alarm
    #        generated.
    # $3 = ceAlarmHistSeverity - This object specifies the severity of the
    #        alarm generated.
    # $4 = ceAlarmHistTimeStamp - This object specifies the value of the
    #        sysUpTime object at the time the alarm was generated.
    ##########
    
    switch ($specific-trap)
    {
        case "1": ### ceAlarmAsserted

            ##########
            # The agent generates this trap when a physical entity asserts an
            # alarm.
            ##########

            $ceAlarmHistEntPhysicalIndex = $1
            $ceAlarmHistAlarmType = $2
            $ceAlarmHistSeverity = $3
            $ceAlarmHistTimeStamp = $4
            details($ceAlarmHistEntPhysicalIndex,$ceAlarmHistAlarmType,$ceAlarmHistSeverity,$ceAlarmHistTimeStamp)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ENTITY-ALARM-MIB-ceAlarmAsserted"

            @AlertGroup = "Physical Entity Status"
            @AlertKey = "entPhysicalEntry." + $1
            @Summary = "Physical Entity Alarm, Alarm type: " + $2 + "  ( " + @AlertKey + " )"
            switch($3)
            {
                case "1": ### critical
                    $SEV_KEY = $OS_EventId + "_critical"
                    $DEFAULT_Severity = 5
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### major
                    $SEV_KEY = $OS_EventId + "_major"
                    $DEFAULT_Severity = 4
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### minor
                    $SEV_KEY = $OS_EventId + "_minor"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### info
                    $SEV_KEY = $OS_EventId + "_info"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 13
                    $DEFAULT_ExpireTime = 1800
                    
                default:
                    $SEV_KEY = $OS_EventId + "_other"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

        case "2": ### ceAlarmCleared

            ##########
            # The agent generates this trap when a physical entity clears a
            # previously asserted alarm.
            ##########

            $ceAlarmHistEntPhysicalIndex = $1
            $ceAlarmHistAlarmType = $2
            $ceAlarmHistSeverity = $3
            $ceAlarmHistTimeStamp = $4
            details($ceAlarmHistEntPhysicalIndex,$ceAlarmHistAlarmType,$ceAlarmHistSeverity,$ceAlarmHistTimeStamp)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ENTITY-ALARM-MIB-ceAlarmCleared"

            @AlertGroup = "Physical Entity Status"
            @AlertKey = "entPhysicalEntry." + $1
            @Summary = "Physical Entity Alarm Cleared, Alarm Type: " + $2 + "  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-ENTITY-ALARM-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-ENTITY-ALARM-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ENTITY-ALARM-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ENTITY-ALARM-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-ENTITY-ALARM-MIB.include.snmptrap.rules >>>>>")
