##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-ISCSI-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-ISCSI-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.10.94.2": ### Cisco iSCSI - Notifications from CISCO-ISCSI-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-ISCSI-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-iSCSI"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### cIscsiTgtLoginFailure

            ##########
            # $1 = cIscsiTgtLoginFailures
            # $2 = cIscsiTgtLastFailureType
            # $3 = cIscsiTgtLastIntrFailureName
            # $4 = cIscsiTgtLastIntrFailureAddrType
            # $5 = cIscsiTgtLastIntrFailureAddr
            ##########

            $cIscsiTgtLoginFailures = $1
            $cIscsiTgtLastFailureType = $2
            $cIscsiTgtLastIntrFailureName = $3
            $cIscsiTgtLastIntrFailureAddrType = lookup($4, InetAddressType) + " ( " + $4 + " )"
            $cIscsiTgtLastIntrFailureAddr = $5
            $cIscsiInstIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $cIscsiNodeIndex = extract($OID1, "\.([0-9]+)$")
            details($cIscsiTgtLoginFailures,$cIscsiTgtLastFailureType,$cIscsiTgtLastIntrFailureName,$cIscsiTgtLastIntrFailureAddrType,$cIscsiTgtLastIntrFailureAddr,$cIscsiInstIndex,$cIscsiNodeIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ISCSI-MIB-cIscsiTgtLoginFailure"

            @AlertGroup = "iSCSI Login Status"
            @AlertKey = "cIscsiTargetAttributesEntry." + $cIscsiInstIndex + "." + $cIscsiNodeIndex
            @Summary = "iSCSI Login Attempt Failed by Target  ( Initiator: " + $5 + " )"
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "2": ### cIscsiIntrLoginFailure

            ##########
            # $1 = cIscsiIntrLoginFailures
            # $2 = cIscsiIntrLastFailureType 
            # $3 = cIscsiIntrLastTgtFailureName
            # $4 = cIscsiIntrLastTgtFailureAddrType
            # $5 = cIscsiIntrLastTgtFailureAddr
            ##########

            $cIscsiIntrLoginFailures = $1
            $cIscsiIntrLastFailureType = $2
            $cIscsiIntrLastTgtFailureName = $3
            $cIscsiIntrLastTgtFailureAddrType = lookup($4, InetAddressType) + " ( " + $4 + " )"
            $cIscsiIntrLastTgtFailureAddr = $5
            $cIscsiInstIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $cIscsiNodeIndex = extract($OID1, "\.([0-9]+)$")
            details($cIscsiIntrLoginFailures,$cIscsiIntrLastFailureType,$cIscsiIntrLastTgtFailureName,$cIscsiIntrLastTgtFailureAddrType,$cIscsiIntrLastTgtFailureAddr,$cIscsiInstIndex,$cIscsiNodeIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ISCSI-MIB-cIscsiIntrLoginFailure"

            @AlertGroup = "iSCSI Login Status"
            @AlertKey = "cIscsiInitiatorAttributesEntry." + $cIscsiInstIndex + "." + $cIscsiNodeIndex
            @Summary = "iSCSI Login Attempt Failed by Initiator  ( Target: " + $5 + " )"
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "3": ### cIscsiInstSessionFailure

            ##########
            # $1 = cIscsiInstSsnFailures
            # $2 = cIscsiInstLastSsnFailureType
            # $3 = cIscsiInstLastSsnRmtNodeName 
            ##########

            $cIscsiInstSsnFailures = $1
            $cIscsiInstLastSsnFailureType = $2
            $cIscsiInstLastSsnRmtNodeName = $3
            $cIscsiInstIndex = extract($OID1, "\.([0-9]+)$")
            details($cIscsiInstSsnFailures,$cIscsiInstLastSsnFailureType,$cIscsiInstLastSsnRmtNodeName)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ISCSI-MIB-cIscsiInstSessionFailure"

            @AlertGroup = "iSCSI Session Status"
            @AlertKey = "cIscsiInstanceAttributesEntry." + $cIscsiInstIndex
            @Summary = "Active iSCSI Session Failed  ( Remote Node: " + $3 + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-ISCSI-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-ISCSI-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ISCSI-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ISCSI-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-ISCSI-MIB.include.snmptrap.rules >>>>>")
