##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCOTRAP-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-CISCOTRAP-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9": ### Cisco General Traps - Traps from CISCOTRAP-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCOTRAP-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-General"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"
    
    switch($specific-trap)
    {
        case "0": ### reload
            
            ##########
            # $1 = sysUpTime 
            # $2 = whyReload 
            ##########
            
            $sysUpTime = $1
            $whyReload = $2
            details($sysUpTime,$whyReload)
            
            $OS_EventId = "SNMPTRAP-cisco-CISCOTRAP-MIB-reload"
            
            @AlertGroup = "System Reload"
            @AlertKey = ""
            @Summary = "System Reinitialized, " + $2
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
            update(@Summary)

        case "1": ### tcpConnectionClose
        
            ##########
            # $1 = tslineSesType - Type of session. (see Lookup table)
            # $2 = tcpConnState - The state of this TCP connection.
            # $3 = loctcpConnElapsed - How long this TCP connection has been
            #        established.
            # $4 = loctcpConnInBytes - Bytes input for this TCP connection.
            # $5 = loctcpConnOutBytes - Bytes output for this TCP connection.
            # $6 = tsLineUser - TACACS user name, if TACACS enabled, of user on
            #        this line.
            ##########
        
            $tslineSesType = lookup($1, tslineSesType)
            $tcpConnState = lookup($2, tcpConnState)
            $loctcpConnElapsed = $3
            $loctcpConnInBytes = $4
            $loctcpConnOutBytes = $5
            if(!exists($6)||match($6, ""))
            {
                $tsLineUser = "Unknown (TACACS not enabled)"
            }
            else
            {
                $tsLineUser = $6
            }
            $tcpConnLocalAddress = extract($OID2, "3\.6\.1\.2\.1\.6\.13\.1\.1\.([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+$")
            $tcpConnLocalPort = extract($OID2, "3\.6\.1\.2\.1\.6\.13\.1\.1\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.([0-9]+)\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+$")
            $tcpConnRemAddress = extract($OID2, "3\.6\.1\.2\.1\.6\.13\.1\.1\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)\.[0-9]+$")
            $tcpConnRemPort = extract($OID2, "3\.6\.1\.2\.1\.6\.13\.1\.1\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+\.([0-9]+)$")
            
            $OS_EventId = "SNMPTRAP-cisco-CISCOTRAP-MIB-tcpConnectionClose"
            
            @AlertGroup = "TCP Connection Status"
            @AlertKey = "tcpConnEntry." + $tcpConnLocalAddress + "." + $tcpConnLocalPort + "." + $tcpConnRemAddress + ".(Remote Port Ignored)"
            @Summary = $tslineSesType + " Connection Terminated  ( From: " + $tcpConnRemAddress + ", User: " + $tsLineUser + " )"
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $6
            
            $tslineSesType = $tslineSesType + " ( " + $1 + " )"
            $tcpConnState = $tcpConnState + " ( " + $2 + " )"
            details($tslineSesType,$tcpConnState,$loctcpConnElapsed,$loctcpConnInBytes,$loctcpConnOutBytes,$tsLineUser,$tcpConnLocalAddress,$tcpConnLocalPort,$tcpConnRemAddress,$tcpConnRemPort)
            
        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCOTRAP-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCOTRAP-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCOTRAP-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCOTRAP-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCOTRAP-MIB.include.snmptrap.rules >>>>>")
