###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-C2900-MIB.include.snmptrap.rules 2.1 
#    2005/03/03 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-C2900-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.87.2": ### Cisco C2900 - Catalyst Notifications from CISCO-C2900-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-C2900-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-C2900"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### c2900AddressViolation
        
            ##########
            # $1 = c2900PortIfIndex 
            ##########
            
            $c2900PortIfIndex = $1
            $c2900PortModuleIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $c2900PortIndex = extract($OID1, "\.([0-9]+)$")
            details($c2900PortIfIndex,$c2900PortModuleIndex,$c2900PortIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-C2900-MIB-c2900AddressViolation"

            @AlertGroup = "Port Status"
            @AlertKey = "c2900PortEntry." + extract($OID1, "\.([0-9]+\.[0-9]+)$")
            @Summary = "Secured Port Address Violation  ( Slot: " + $c2900PortModuleIndex + ", Port: " + $c2900PortIndex + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
        
        case "2": ### c2900BroadcastStorm
            
            ##########
            # $1 = c2900PortBroadcastRisingThreshold 
            ##########
            
            $c2900PortBroadcastRisingThreshold = $1
            $c2900PortModuleIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $c2900PortIndex = extract($OID1, "\.([0-9]+)$")
            details($c2900PortBroadcastRisingThreshold,$c2900PortModuleIndex,$c2900PortIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-C2900-MIB-c2900BroadcastStorm"

            @AlertGroup = "Port Status"
            @AlertKey = "c2900PortEntry." + extract($OID1, "\.([0-9]+\.[0-9]+)$") ### c2900PortModuleIndex.c2900PortIndex
            @Summary = "Port Broadcast Packets Exceeds " + $1 + " frames/sec.  ( Slot: " + $c2900PortModuleIndex + ", Port: " + $c2900PortIndex + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

                        
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
        
        case "3": ### c2900RpsFailed

            ##########
            # $1 = sysName
            ##########

            $sysName = $1
            details($sysName)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-C2900-MIB-c2900RpsFailed"

            @AlertGroup = "Redundant Power System Status"
            @AlertKey = ""
            @Summary = "Redundant Power System Failure"
            
            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-C2900-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-C2900-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-C2900-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-C2900-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-C2900-MIB.include.snmptrap.rules >>>>>")
