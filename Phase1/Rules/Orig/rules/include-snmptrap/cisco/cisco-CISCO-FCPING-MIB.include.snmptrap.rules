##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-FCPING-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-FCPING-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.295.1.3": ### Cisco Fibre Channel Ping - Notifications from CISCO-FCPING-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-FCPING-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Fibre Channel Ping"
    @Class = "40057"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### fcPingCompletionNotify

            ##########
            # $1 = fcPingAddress
            # $2 = fcPingTxPackets 
            # $3 = fcPingRxPackets 
            ##########

            $fcPingAddress = $1
            $fcPingTxPackets = $2
            $fcPingRxPackets = $3
            $fcPingIndex = extract($OID1, "\.([0-9]+)$")
            details($fcPingAddress,$fcPingTxPackets,$fcPingRxPackets)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-FCPING-MIB-fcPingCompletionNotify"

            @AlertGroup = "Fibre Channel Ping Status"
            @AlertKey = "fcPingEntry." + $fcPingIndex
            if(match($2, $3))
            {
                $status = "success"
                @Summary = "Fibre Channel Ping Successful, Received " + $3 + " of " + $2 + "Sent Packets"
                
                $SEV_KEY = $OS_EventId + "_success"
                $DEFAULT_Severity = 1
                $DEFAULT_Type = 2
                $DEFAULT_ExpireTime = 0
            }
            else if(match($3, "0"))
            {
                $status = "failure"
                @Summary = "Fibre Channel Ping Failed, Received " + $3 + " of " + $2 + "Sent Packets"
                
                $SEV_KEY = $OS_EventId + "_failure"
                $DEFAULT_Severity = 3
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
            else
            {
                $status = "errors"
                @Summary = "Fibre Channel Ping Errors, Received " + $3 + " of " + $2 + "Sent Packets"
                
                $SEV_KEY = $OS_EventId + "_errors"
                $DEFAULT_Severity = 2
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
            @Summary = @Summary + "  ( Target: " + $3 + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $status

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-FCPING-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-FCPING-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FCPING-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FCPING-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-FCPING-MIB.include.snmptrap.rules >>>>>")
