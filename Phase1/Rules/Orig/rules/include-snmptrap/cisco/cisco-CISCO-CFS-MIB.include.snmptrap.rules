###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-CFS-MIB.include.snmptrap.rules 1.0
#    2006/04/13 Rajyalakshmi Reddy
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-CFS-MIB
#
###############################################################################

case ".1.3.6.1.4.1.9.9.433": ### Cisco CFS (Cisco Fabric Service) - Notifications from CISCO-CFS-MIB (20041224)

    log(DEBUG, "<<<<< Entering... cisco-CISCO-CFS-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Cisco Fabric Service"
    @Class = "40057"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### ciscoCFSFeatureActionNotif

            ##########
            # $1 = cfsFeatureOpLastAction 
            # $2 = cfsFeatureOpLastScopeType 
            # $3 = cfsFeatureOpLastScopeVal 
            # $4 = cfsFeatureOpLastActionResult 
            ##########

            $cfsFeatureOpLastAction = lookup($1, CiscoCFSAction)
            $cfsFeatureOpLastScopeType = lookup($2, CiscoCFSScopeType) + " ( " + $2 + " )"
            $cfsFeatureOpLastScopeVal = $3
            $cfsFeatureOpLastActionResult = lookup($4, CiscoCFSFeatureActionResult) + " ( " + $4 + " )"
            
            $cfsFeatureOpName_Raw = extract($OID1, "3\.6\.1\.4\.1\.9\.9\.433\.1\.1\.1\.1\.5\.(.*)$")
            $OctetString = $cfsFeatureOpName_Raw
            include "$NC_RULES_HOME/include-snmptrap/decodeOctetString.include.snmptrap.rules"
            $cfsFeatureOpName = $String

            $OS_EventId = "SNMPTRAP-cisco-CISCO-CFS-MIB-ciscoCFSFeatureActionNotif"

            @AlertGroup = "CFS " + $cfsFeatureOpLastAction + " Status"
            @AlertKey = "cfsFeatureOpEntry." + $cfsFeatureOpName_Raw
            switch($1)
            {
                case "1":### noop
                    $SEV_KEY = $OS_EventId + "_noop"
                case "2":### enable 
                    $SEV_KEY = $OS_EventId + "_enable"
                case "3":### disable 
                    $SEV_KEY = $OS_EventId + "_disable"
                case "4":### commit 
                    $SEV_KEY = $OS_EventId + "_commit"
                case "5":### abort 
                    $SEV_KEY = $OS_EventId + "_abort"
                case "6":### clear
                    $SEV_KEY = $OS_EventId + "_clear"
                default: 
                    $SEV_KEY = $OS_EventId + "_unknown"
            } 

            switch($4)
            {
                case "1":### none
                    $SEV_KEY = $SEV_KEY + "_none"
                    @Summary = "CFS " + $cfsFeatureOpLastAction + " Status None" 

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "2":### success 
                    $SEV_KEY = $SEV_KEY + "_success"
                    @Summary = "CFS " + $cfsFeatureOpLastAction + " Successful" 

                    $DEFAULT_Severity = 1 
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "3":### failed 
                    $SEV_KEY = $SEV_KEY + "_failed"
                    @Summary = "CFS " + $cfsFeatureOpLastAction + " Failed" 

                    $DEFAULT_Severity = 3 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "4":### inProgress 
                    $SEV_KEY = $SEV_KEY + "_inProgress"
                    @Summary = "CFS " + $cfsFeatureOpLastAction + " In Progress" 

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                default: 
                    $SEV_KEY = $SEV_KEY + "_unknown"
                    @Summary = "CFS " + $cfsFeatureOpLastAction + " Status Unknown" 

                    $DEFAULT_Severity = 2 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            } 
         
            @Summary = @Summary + "  ( CFS Feature: " + $cfsFeatureOpName + " )"  

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $4

            $cfsFeatureOpLastAction = $cfsFeatureOpLastAction + " ( " + $1 + " )" 
            details($cfsFeatureOpLastAction,$cfsFeatureOpLastScopeType,$cfsFeatureOpLastScopeVal,$cfsFeatureOpLastActionResult,$cfsFeatureOpName)

        case "2": ### ciscoCFSMergeFailNotif

            ##########
            # $1 = cfsMergeFailFeatureName 
            # $2 = cfsMergeFailScopeType 
            # $3 = cfsMergeFailScopeVal 
            # $4 = cfsMergeFailReasonDescription 
            ##########

            $cfsMergeFailFeatureName = $1
            $cfsMergeFailScopeType = lookup($2, CiscoCFSScopeType) + " ( " + $2 + " )"
            $cfsMergeFailScopeVal = $3 
            $cfsMergeFailReasonDescription = $4

            $OS_EventId = "SNMPTRAP-cisco-CISCO-CFS-MIB-ciscoCFSMergeFailNotif"

            @AlertGroup = "CFS Merge Status"
            @AlertKey = "Feature: " + $1
            @Summary = "CFS Merge Failed"
            if(!match($4, ""))
             {
               @Summary = @Summary + ", " + $4
             }
            @Summary = @Summary + "  ( " + @AlertKey + " )"

            $DEFAULT_Severity = 3 
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $4

            details($cfsMergeFailFeatureName,$cfsMergeFailScopeType,$cfsMergeFailScopeVal,$cfsMergeFailReasonDescription)

        case "3": ### ciscoCFSDiscoveryCompleteNotif

            ##########
            # $1 = cfsPeersDiscoveryResult 
            # $2 = cfsPeersDiscoveryFailureReason 
            ##########

            $cfsPeersDiscoveryResult = lookup($1, cfsPeersDiscoveryResult) + " ( " + $1 + " )"
            $cfsPeersDiscoveryFailureReason = $2

            $OS_EventId = "SNMPTRAP-cisco-CISCO-CFS-MIB-ciscoCFSDiscoveryCompleteNotif"

            @AlertGroup = "CFS Discovery Status"
            @AlertKey = ""
            switch($1)
            {
                case "1":### notInitiated 
                    $SEV_KEY = $OS_EventId + "_notInitiated"
                    @Summary = "CFS Discovery Not Initiated"

                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                case "2":### inProgress 
                    $SEV_KEY = $OS_EventId + "_inProgress"
                    @Summary = "CFS Discovery In Progress"

                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                case "3":### successful 
                    $SEV_KEY = $OS_EventId + "_successful"
                    @Summary = "CFS Discovery Successful"

                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "4":### failed 
                    $SEV_KEY = $OS_EventId + "_failed"
                    @Summary = "CFS Discovery Failed, " + $2

                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                default:
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "CFS Discovery Status Unknown"

                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1 + " " + $2

            details($cfsPeersDiscoveryResult,$cfsPeersDiscoveryFailureReason)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-CFS-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-CFS-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-CFS-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-CFS-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-CFS-MIB.include.snmptrap.rules >>>>>")
