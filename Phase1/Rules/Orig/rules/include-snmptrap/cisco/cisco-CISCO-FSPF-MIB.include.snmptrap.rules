###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-FSPF-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-FSPF-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.287.3": ### Cisco Fabric Shortest Path First - Notifications from CISCO-FSPF-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-FSPF-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Fabric Shortest Path First"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### fspfNbrStateChangeNotify

            ##########
            # $1 = ifIndex 
            # $2 = notifyVsanIndex
            # $3 = fspfDomainId 
            # $4 = fspfIfNbrDomainId
            # $5 = fspfIfNbrState
            # $6 = fspfIfPrevNbrState 
            ##########

            $ifIndex = $1
            $notifyVsanIndex = $2
            $fspfDomainId = $3
            $fspfIfNbrDomainId = $4
            $fspfIfNbrState = lookup($5, FspfInterfaceState) + " ( " + $5 + " )"
            $fspfIfPrevNbrState = lookup($6, FspfInterfaceState) + " ( " + $6 + " )"
            details($ifIndex,$notifyVsanIndex,$fspfDomainId,$fspfIfNbrDomainId,$fspfIfNbrState,$fspfIfPrevNbrState)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-FSPF-MIB-fspfNbrStateChangeNotify"

            @AlertGroup = "FSPF Neighbor Status"
            @AlertKey = "fspfIfEntry." + $2 + "." + $1
            switch($5)
            {
                case "1": ### down
                    @Summary = "FSPF Neighbor Down"
                    
                    $SEV_KEY = $OS_EventId + "_down"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### init
                    @Summary = "FSPF Neighbor Initializing"
                    
                    $SEV_KEY = $OS_EventId + "_init"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### dbExchange
                    @Summary = "FSPF Neighbor Database Exchange"
                    
                    $SEV_KEY = $OS_EventId + "_dbExchange"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### dbAckwait
                    @Summary = "FSPF Neighbor Waiting for Database Acknowledgement"
                    
                    $SEV_KEY = $OS_EventId + "_dbAckwait"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "5": ### dbWait
                    @Summary = "FSPF Neighbor Waiting for Database"
                    
                    $SEV_KEY = $OS_EventId + "_dbWait"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "6": ### full
                    @Summary = "FSPF Neighbor Connected"
                    
                    $SEV_KEY = $OS_EventId + "_full"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "FSPF Neighbor Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $5

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-FSPF-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-FSPF-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FSPF-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FSPF-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-FSPF-MIB.include.snmptrap.rules >>>>>")
