###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    bridgewater-BW-NOTIFICATION.include.snmptrap.rules 1.0 
#    2005/11/10 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  BW-NOTIFICATION
#
###############################################################################

case ".1.3.6.1.4.1.3631.1.5": ### Bridgewater RADIUS Server - Notifications from BW-NOTIFICATION (001207)

    log(DEBUG, "<<<<< Entering... bridgewater-BW-NOTIFICATION.include.snmptrap.rules >>>>>")

    @Agent = "Bridgewater-RADIUS Server"
    @Class = ""

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### notificationGeneric

            ##########
            # $1 = notificationOID
            # $2 = notificationGenericInfo 
            ##########

            $notificationOID = $1
            $notificationGenericInfo = $2

            $OS_EventId = "SNMPTRAP-bridgewater-BW-NOTIFICATION-notificationGeneric"

            @AlertGroup = "Generic Notification"
            @AlertKey = "OID: " + $1
            @Summary = $2 + "  ( " + @AlertKey + " )"

            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($notificationOID,$notificationGenericInfo)

        case "2": ### notificationProcess

            ##########
            # $1 = notificationOID 
            # $2 = notificationProcessState 
            # $3 = notificationGenericInfo 
            ##########

            $notificationOID = $1
            $notificationProcessState = lookup($2, bridgewaterNotificationProcessState) + " ( " + $2 + " )"
            $notificationGenericInfo = $3

            $OS_EventId = "SNMPTRAP-bridgewater-BW-NOTIFICATION-notificationProcess"

            @AlertGroup = "Process Status"
            @AlertKey = "OID: " + $1
            switch($2)
            {
                case "1": ### starting
                    $SEV_KEY = $OS_EventId + "_starting"
                    @Summary = "Process Starting"
        
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "2": ### stopping
                    $SEV_KEY = $OS_EventId + "_stopping"
                    @Summary = "Process Stopping"
        
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 11
                    $DEFAULT_ExpireTime = 0
                case "3": ### restarting
                    $SEV_KEY = $OS_EventId + "_restarting"
                    @Summary = "Process Restarting"
        
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "4": ### stopped
                    $SEV_KEY = $OS_EventId + "_stopped"
                    @Summary = "Process Stopped"
        
                    $DEFAULT_Severity = 4
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                default:
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "Process Status Unknown"
        
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }
            if(!match($3, ""))
            {
                @Summary = @Summary + ", " + $3
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

            details($notificationOID,$notificationProcessState,$notificationGenericInfo)

        case "3": ### notificationConnection

            ##########
            # $1 = notificationOID 
            # $2 = notificationConnectionState 
            # $3 = notificationGenericInfo 
            ##########

            $notificationOID = $1
            $notificationConnectionState = lookup($2, bridgewaterNotificationConnectionState) + " ( " + $2 + " )"
            $notificationGenericInfo = $3

            $OS_EventId = "SNMPTRAP-bridgewater-BW-NOTIFICATION-notificationConnection"

            @AlertGroup = "Connection Status"
            @AlertKey = "OID: " + $1
            switch($2)
            {
                case "1": ### up
                    $SEV_KEY = $OS_EventId + "_up"
                    @Summary = "Connection Up"
        
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "2": ### down
                    $SEV_KEY = $OS_EventId + "_down"
                    @Summary = "Connection Down"
        
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                default:
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "Connection Status Unknown"
        
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }
            if(!match($3, ""))
            {
                @Summary = @Summary + ", " + $3
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

            details($notificationOID,$notificationConnectionState,$notificationGenericInfo)

        case "4": ### notificationThreshold

            ##########
            # $1 = notificationOID 
            # $2 = notificationThresholdState 
            # $3 = notificationThresholdValue 
            # $4 = notificationCurrentValue
            # $5 = notificationInterval
            # $6 = notificationGenericInfo 
            ##########

            $notificationOID = $1
            $notificationThresholdState = lookup($2, bridgewaterNotificationThresholdState) + " ( " + $2 + " )"
            $notificationThresholdValue = $3
            $notificationCurrentValue = $4
            $notificationInterval = $5
            $notificationGenericInfo = $6

            $OS_EventId = "SNMPTRAP-bridgewater-BW-NOTIFICATION-notificationThreshold"

            @AlertGroup = "Threshold Status"
            @AlertKey = "OID: " + $1
            switch($2)
            {
                case "1": ### thresholdCrossed
                    $SEV_KEY = $OS_EventId + "_thresholdCrossed"
                    @Summary = "Threshold Crossed: " + $4 + " Crossed " + $3
        
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                default:
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "Threshold Status Unknown"
        
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }
            if(!match($6, ""))
            {
                @Summary = @Summary + ", " + $6
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

            details($notificationOID,$notificationThresholdState,$notificationThresholdValue,$notificationCurrentValue,$notificationInterval,$notificationGenericInfo)

        case "5": ### notificationDbError

            ##########
            # $1 = notificationOID
            # $2 = notificationDbErrorState 
            # $3 = notificationGenericInfo
            ##########

            $notificationOID = $1
            $notificationDbErrorState = lookup($2, bridgewaterNotificationDbErrorState) + " ( " + $2 + " )"
            $notificationGenericInfo = $3

            $OS_EventId = "SNMPTRAP-bridgewater-BW-NOTIFICATION-notificationDbError"

            @AlertGroup = "Database Status"
            @AlertKey = "OID: " + $1
            switch($2)
            {
                case "1": ### recoverableOperation
                    $SEV_KEY = $OS_EventId + "_recoverableOperation"
                    @Summary = "Database Recoverable Operation"
        
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                case "2": ### fatalError
                    $SEV_KEY = $OS_EventId + "_fatalError"
                    @Summary = "Database Fatal Error"
        
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                default:
                    $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "Database Status Unknown"
        
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
            }
            if(!match($3, ""))
            {
                @Summary = @Summary + ", " + $3
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

            details($notificationOID,$notificationDbErrorState,$notificationGenericInfo)

        case "6": ### notificationAuthError

            ##########
            # $1 = notificationOID 
            # $2 = notificationIPAddress 
            # $3 = notificationPort 
            ##########

            $notificationOID = $1
            $notificationIPAddress = $2
            $notificationPort = $3

            $OS_EventId = "SNMPTRAP-bridgewater-BW-NOTIFICATION-notificationAuthError"

            @AlertGroup = "Authentication Status"
            @AlertKey = "OID: " + $1
            @Summary = "Security Breach, Unexpected Message Received  ( From IP: " + $2 + ", Port: " + $3 + " )"

            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2 + " " + $3

            details($notificationOID,$notificationIPAddress,$notificationPort)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, bridgewater-BW-NOTIFICATION_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, bridgewater-BW-NOTIFICATION_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/bridgewater/bridgewater-BW-NOTIFICATION.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/bridgewater/bridgewater-BW-NOTIFICATION.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

log(DEBUG, "<<<<< Leaving... bridgewater-BW-NOTIFICATION.include.snmptrap.rules >>>>>")
