###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-STP-EXTENSIONS-MIB.include.snmptrap.rules 2.1
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-STP-EXTENSIONS-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.82.2": ### Cisco 802.1D Spanning Tree Protocol (STP) Extensions - Cisco from CISCO-STP-EXTENSIONS-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-STP-EXTENSIONS-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-STP Extensions"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"
    
    switch($specific-trap)
    {
        case "1": ### stpxInconsistencyUpdate

            ##########
            # $1 = stpxInconsistentState 
            ##########

            $stpxInconsistentState = $1
            $stpxVlanIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $stpxPortIndex = extract($OID1, "\.([0-9]+)$")
            details($stpxInconsistentState,$stpxVlanIndex,$stpxPortIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STP-EXTENSIONS-MIB-stpxInconsistencyUpdate"

            @AlertGroup = "VLAN Port STP Consistency"
            @AlertKey = "stpxInconsistencyEntry." + $stpxVlanIndex + "." + $stpxPortIndex
            switch($1)
            {
                case "0":
                    @Summary = "VLAN Port Spanning Tree Consistent"
                    
                    $SEV_KEY = $OS_EventId + "_consistent"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "1": ### typeInconsistent
                    @Summary = "VLAN Port Spanning Tree Port-Type Inconsistency"
                    
                    $SEV_KEY = $OS_EventId + "_typeInconsistent"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### pvidInconsistent
                    @Summary = "VLAN Port Spanning Tree PVID Inconsistency"
                    
                    $SEV_KEY = $OS_EventId + "_pvidInconsistent"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "VLAN Port Spanning Tree Consistency Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + " ( VLAN: " + $stpxVlanIndex + ", Port: " + $stpxPortIndex + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        case "2": ### stpxRootInconsistencyUpdate

            ##########
            # $1 = stpxRootInconsistencyState
            # $2 = stpxSpanningTreeType 
            ##########

            $stpxRootInconsistencyState = lookup($1, TruthValue) + " ( " + $1 + " )"
            $stpxSpanningTreeType = lookup($2, stpxSpanningTreeType)
            $stpxRootInconsistencyIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $stpxRootInconsistencyPortIndex = extract($OID1, "\.([0-9]+)$")
            
            $OS_EventId = "SNMPTRAP-cisco-CISCO-STP-EXTENSIONS-MIB-stpxRootInconsistencyUpdate"

            @AlertGroup = "VLAN Port STP Root Consistency"
            @AlertKey = "stpxRootInconsistencyEntry." + $stpxRootInconsistencyIndex + "." + $stpxRootInconsistencyPortIndex
            switch($1)
            {
                case "1": ### true
                    @Summary = "VLAN Port " + $stpxSpanningTreeType + " Spanning Tree Root Inconsistency"
                    
                    $SEV_KEY = $OS_EventId + "_true"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### false
                    @Summary = "VLAN Port " + $stpxSpanningTreeType + " Spanning Tree Root Consistent"
                    
                    $SEV_KEY = $OS_EventId + "_false"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "VLAN Port " + $stpxSpanningTreeType + " Spanning Tree Root Consistency Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + " ( VLAN: " + $stpxRootInconsistencyIndex + ", Port: " + $stpxRootInconsistencyPortIndex + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $stpxSpanningTreeType = $stpxSpanningTreeType + " ( " + $2 + " )"
            details($stpxRootInconsistencyState,$stpxSpanningTreeType,$stpxRootInconsistencyIndex,$stpxRootInconsistencyPortIndex)
        
        case "3": ### stpxLoopInconsistencyUpdate

            ##########
            # $1 = stpxLoopInconsistencyState
            # $2 = stpxSpanningTreeType 
            ##########

            $stpxLoopInconsistencyState = lookup($1, TruthValue) + " ( " + $1 + " )"
            $stpxSpanningTreeType = lookup($2, stpxSpanningTreeType)
            $stpxLoopInconsistencyIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $stpxLoopInconsistencyPortIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-STP-EXTENSIONS-MIB-stpxLoopInconsistencyUpdate"

            @AlertGroup = "VLAN Port STP Loop Consistency"
            @AlertKey = "stpxLoopInconsistencyEntry." + $stpxLoopInconsistencyIndex + "." + $stpxLoopInconsistencyPortIndex
            switch($1)
            {
                case "1": ### true
                    @Summary = "VLAN Port " + $stpxSpanningTreeType + " Spanning Tree Loop Inconsistency"
                    
                    $SEV_KEY = $OS_EventId + "_true"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### false
                    @Summary = "VLAN Port " + $stpxSpanningTreeType + " Spanning Tree Loop Consistent"
                    
                    $SEV_KEY = $OS_EventId + "_false"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "VLAN Port " + $stpxSpanningTreeType + " Spanning Tree Loop Consistency Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $stpxSpanningTreeType = $stpxSpanningTreeType + " ( " + $2 + " )"
            details($stpxLoopInconsistencyState,$stpxSpanningTreeType,$stpxLoopInconsistencyIndex,$stpxLoopInconsistencyPortIndex)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-STP-EXTENSIONS-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-STP-EXTENSIONS-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-STP-EXTENSIONS-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-STP-EXTENSIONS-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-STP-EXTENSIONS-MIB.include.snmptrap.rules >>>>>")
