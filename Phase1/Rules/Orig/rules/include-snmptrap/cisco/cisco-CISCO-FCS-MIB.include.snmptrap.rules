##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-FCS-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-FCS-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.297.1.4": ### Cisco Fabric Configuration Server - Notifications from CISCO-FCS-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-FCS-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Fabric Configuration Server"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### fcsReqRejNotify

            ##########
            # $1 = fcsRejReasonCode
            # $2 = fcsRejReasonCodeExplanation 
            ##########

            $fcsRejReasonCode = lookup($1, FcGs3RejectReasonCode)
            $fcsRejReasonCodeExplanation = lookup($2, FcFcsRejReasonExplanation) + " ( " + $2 + " )"
            
            $OS_EventId = "SNMPTRAP-cisco-CISCO-FCS-MIB-fcsReqRejNotify"

            @AlertGroup = "FCS Request Status"
            @AlertKey = ""
            @Summary = "Fabric Configuration Server Request Rejected, " + $fcsRejReasonCode
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $fcsRejReasonCode = $fcsRejReasonCode + " ( " + $1 + " )"
            details($fcsRejReasonCode,$fcsRejReasonCodeExplanation)

        case "2": ### fcsDiscoveryCompleteNotify

            ##########
            # $1 = fcsVsanDiscoveryName
            ##########

            $fcsVsanDiscoveryName = $1
            details($fcsVsanDiscoveryName)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-FCS-MIB-fcsDiscoveryCompleteNotify"

            @AlertGroup = "Discovery Cycle Status"
            @AlertKey = "Discovery: " + $1
            @Summary = "Discover Cycle Complete  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-FCS-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-FCS-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FCS-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FCS-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-FCS-MIB.include.snmptrap.rules >>>>>")
