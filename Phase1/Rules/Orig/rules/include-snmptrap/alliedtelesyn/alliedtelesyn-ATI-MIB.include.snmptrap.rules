###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    alliedtelesyn-ATI-MIB.include.snmptrap.rules 2.0 
#    2005/10/10 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  ATI-MIB
#
###############################################################################
#
# 2.0 - Improved details of event presentation.
#
#     - Added basic debug logging.
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
###############################################################################

case ".1.3.6.1.4.1.207.1.2": ### Allied Telesyn Repeater - Traps from ATI-MIB

    log(DEBUG, "<<<<< Entering... alliedtelesyn-ATI-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Allied Telesyn-Repeater"
    @Class = "87007"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "20": ### intruderProtectionTrap

            ##########
            # $1 = rptrAddrTrackNewLastSrcAddress 
            ##########

            $rptrAddrTrackNewLastSrcAddress = $1
            $rptrAddrTrackGroupIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $rptrAddrTrackPortIndex = extract($OID1, "\.([0-9]+)$")
            details($rptrAddrTrackNewLastSrcAddress,$rptrAddrTrackGroupIndex,$rptrAddrTrackPortIndex)

            $OS_EventId = "SNMPTRAP-alliedtelesyn-ATI-MIB-intruderProtectionTrap"

            @AlertGroup = "Intruder Protection"
            @AlertKey = "rptrAddrTrackEntry." + $rptrAddrTrackGroupIndex + "." + $rptrAddrTrackPortIndex
            @Summary = "Intruder Alert, Unauthorized Source MAC Address: " + $1 + "  ( Group: " + $rptrAddrTrackGroupIndex + ", Port: " + $rptrAddrTrackPortIndex + " )"
            
            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        case "21": ### atiIntruderProtectionTrap

            ##########
            # $1 = atiTrapIntruderModuleNumber 
            # $2 = atiTrapIntruderPortNumber 
            # $3 = atiTrapIntruderMacAddress 
            ##########

            $atiTrapIntruderModuleNumber = $1
            $atiTrapIntruderPortNumber = $2
            $atiTrapIntruderMacAddress = $3
            details($atiTrapIntruderModuleNumber,$atiTrapIntruderPortNumber,$atiTrapIntruderMacAddress)

            $OS_EventId = "SNMPTRAP-alliedtelesyn-ATI-MIB-atiIntruderProtectionTrap"

            @AlertGroup = "Intruder Protection"
            @AlertKey = "Module: " + $1 + ", Port: " + $2
            @Summary = "Intruder Alert, Unauthorized Source MAC Address: " + $1 + "  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        case "22": ### atiRedundantPortChangeTrap

            ##########
            # $1 = atiPortGroupIndex 
            # $2 = atiPortIndex 
            # $3 = portRedundantPort 
            # $4 = atiPortChangeDirection 
            ##########

            $atiPortGroupIndex = $1
            $atiPortIndex = $2
            $portRedundantPort = $3
            $atiPortChangeDirection = lookup($4, atiPortChangeDirection) + " ( " + $4 + " )"
            details($atiPortGroupIndex,$atiPortIndex,$portRedundantPort,$atiPortChangeDirection)

            $OS_EventId = "SNMPTRAP-alliedtelesyn-ATI-MIB-atiRedundantPortChangeTrap"

            @AlertGroup = "Redundant Port Change"
            @AlertKey = "atiPortEntry." + $1 + "." + $2
            switch($4)
            {
                case "1": ### Primary to Secondary
                    @Summary = "Redundant Port, " + $3 + ", Changed from Primary to Secondary"
                case "2": ### Secondary to Primary
                    @Summary = "Redundant Port, " + $3 + ", Changed from Secondary to Primary"
                default:
                    @Summary = "Redundant Port, " + $3 + ", Status Unknown"
            }
            @Summary = @Summary + "  ( Group: " + $1 + ", Port: " + $2 + " )"
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3 + " " + $4

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, alliedtelesyn-ATI-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, alliedtelesyn-ATI-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/alliedtelesyn/alliedtelesyn-ATI-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/alliedtelesyn/alliedtelesyn-ATI-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... alliedtelesyn-ATI-MIB.include.snmptrap.rules >>>>>")
