##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-IP-ENCRYPTION-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-IP-ENCRYPTION-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.52.2": ### Cisco IP encryption - Notifications from CISCO-IP-ENCRYPTION-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-IP-ENCRYPTION-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-IP encryption"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### cieTestCompletion
    
            ##########
            # $1 = cieTestConnSessionStatus
            # $2 = cieTestConnProtectedAddr
            # $3 = cieTestConnUnprotectedAddr 
            ##########
        
            $cieTestConnSessionStatus = lookup($1, cieTestConnSessionStatus) + " ( " + $1 + " )"
            $cieTestConnProtectedAddr = $2
            $cieTestConnUnprotectedAddr = $3
            $cieTestConnSerialNumber = extract($OID1, "\.([0-9]+)$")
            details($cieTestConnSessionStatus,$cieTestConnProtectedAddr,$cieTestConnUnprotectedAddr)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-IP-ENCRYPTION-MIB-cieTestCompletion"

            @AlertGroup = "Encrypted Session Status"
            @AlertKey = "cieTestConnEntry." + $cieTestConnSerialNumber
            switch($1)
            {
                case "1": ### inProgress
                    @Summary = "Encrypted Session Establishment In Progress"
                    
                    $SEV_KEY = $OS_EventId + "_inProgress"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### fail
                    @Summary = "Encrypted Session Failed"
                    
                    $SEV_KEY = $OS_EventId + "_fail"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### success
                    @Summary = "Encrypted Session Successful"
                    
                    $SEV_KEY = $OS_EventId + "_success"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### badCryptoMapName
                    @Summary = "Encrypted Session Failed, Bad Crypto Map Name"
                    
                    $SEV_KEY = $OS_EventId + "_badCryptoMapName"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Encrypted Session Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( Protected IP: " + $2 + ", Unprotected IP: " + $3 + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-IP-ENCRYPTION-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-IP-ENCRYPTION-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-IP-ENCRYPTION-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-IP-ENCRYPTION-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-IP-ENCRYPTION-MIB.include.snmptrap.rules >>>>>")
