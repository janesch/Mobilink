###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    IETF-DISMAN-SCHEDULE-MIB.include.snmptrap.rules 3.1
#    2005/03/06 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  IETF-DISMAN-SCHEDULE-MIB
#
###############################################################################

log(DEBUG, "<<<<< Entering... IETF-DISMAN-SCHEDULE-MIB.include.snmptrap.rules >>>>>")

case ".1.3.6.1.2.1.63.2": ### Distributed Management Scheduling - Notifications from DISMAN-SCHEDULE-MIB (RFC2591)

    @Agent = "IETF-DISMAN-SCHEDULE-MIB"
    @Class = "40086"

    $OPTION_TypeFieldUsage = "3.6"

    switch ($specific-trap)
    {
        case "1": ### schedActionFailure

            ##########
            # $1 = schedLastFailure
            # $2 = schedLastFailed
            ##########

            $schedLastFailure = lookup($1, SnmpPduErrorStatus)
            $schedLastFailed = $2
            
            $OS_EventId = "SNMPTRAP-IETF-DISMAN-SCHEDULE-MIB-schedActionFailure"

            @AlertGroup = "Scheduled Action Status"
            @AlertKey = "schedEntry." + extract($OID1, "3\.6\.1\.2\.1\.63\.1\.2\.1\.17\.(.*)$")
            switch($1)
            {
                case "0":
                    @Summary = "Scheduled Action Successful  ( " + @AlertKey + " )"
                    
                    $SEV_KEY = $OS_EventId + "_noError"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0

                default:
                    @Summary = "Scheduled Action Failed, " + $schedLastFailure + "  ( " + @AlertKey + " )"
                    
                    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-DISMAN-SCHEDULE-MIB_sev)
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $schedLastFailure = $schedLastFailure + " ( " + $1 + " )"
            details($schedLastFailure,$schedLastFailed)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, IETF-DISMAN-SCHEDULE-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-DISMAN-SCHEDULE-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-DISMAN-SCHEDULE-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-DISMAN-SCHEDULE-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... IETF-DISMAN-SCHEDULE-MIB.include.snmptrap.rules >>>>>")
