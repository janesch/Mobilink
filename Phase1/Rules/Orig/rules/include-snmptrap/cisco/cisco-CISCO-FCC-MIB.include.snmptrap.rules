##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-FCC-MIB.include.snmptrap.rules 1.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-FCC-MIB
#
###############################################################################
#
# 1.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.365": ### Cisco Fibre Channel Congestion Control - Notifications from CISCO-FCC-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-FCC-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Fibre Channel Congestion Control"
    @Class = "40057"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### ciscoFCCCongestionStateChange

            ##########
            # $1 = ifIndex 
            # $2 = cFCCCongestionState 
            ##########

            $ifIndex = $1
            $cFCCCongestionState = lookup($2, CiscoFCCCongestionState) + " ( " + $2 + " )"
            details($ifIndex,$cFCCCongestionState)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-FCC-MIB-ciscoFCCCongestionStateChange"

            @AlertGroup = "Fibre Channel Port Congestion Status"
            @AlertKey = "ifEntry." + $1
            switch($2)
            {
                case "1": ### noCongestion
                    @Summary = "Fibre Channel Port No Congestion"
                    
                    $SEV_KEY = $OS_EventId + "_noCongestion"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### congested
                    @Summary = "Fibre Channel Port Congested"
                    
                    $SEV_KEY = $OS_EventId + "_congested"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### severelyCongested
                    @Summary = "Fibre Channel Port Severely Congested"
                    
                    $SEV_KEY = $OS_EventId + "_severelyCongested"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Fibre Channel Port Congestion Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2

        case "2": ### ciscoFCCCongestionRateLimitStart

            ##########
            # $1 = ifIndex 
            # $2 = cFCCCongestionSourceID
            # $3 = cFCCCongestionDestinationID 
            # $4 = cFCCCongestionNotifyVSANIndex 
            ##########

            $ifIndex = $1
            $cFCCCongestionSourceID = $2
            $cFCCCongestionDestinationID = $3
            $cFCCCongestionNotifyVSANIndex = $4
            details($ifIndex,$cFCCCongestionSourceID,$cFCCCongestionDestinationID,$cFCCCongestionNotifyVSANIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-FCC-MIB-ciscoFCCCongestionRateLimitStart"

            @AlertGroup = "Fibre Channel Port Rate Limit Status"
            @AlertKey = "Port: ifEntry." + $1 + ", VSAN: vsanEntry. " + $4 + ", Src: " + $2 + ", Dest: " + $3
            @Summary = "Fibre Channel Port Rate Limiting Started  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "3": ### ciscoFCCCongestionRateLimitEnd

            ##########
            # $1 = ifIndex 
            # $2 = cFCCCongestionSourceID
            # $3 = cFCCCongestionDestinationID 
            # $4 = cFCCCongestionNotifyVSANIndex
            ##########

            $ifIndex = $1
            $cFCCCongestionSourceID = $2
            $cFCCCongestionDestinationID = $3
            $cFCCCongestionNotifyVSANIndex = $4
            details($ifIndex,$cFCCCongestionSourceID,$cFCCCongestionDestinationID,$cFCCCongestionNotifyVSANIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-FCC-MIB-ciscoFCCCongestionRateLimitEnd"

            @AlertGroup = "Fibre Channel Port Rate Limit Status"
            @AlertKey = "Port: ifEntry." + $1 + ", VSAN: vsanEntry. " + $4 + ", Src: " + $2 + ", Dest: " + $3
            @Summary = "Fibre Channel Port Rate Limiting Stopped  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-FCC-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-FCC-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FCC-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-FCC-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-FCC-MIB.include.snmptrap.rules >>>>>")
