##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco.generic.include.snmptrap.rules 2.2 
#    2005/03/07 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
###############################################################################
#
# 2.2 - Added basic debug logging.
#
# 2.1 - Fixed a bug which caused generic traps from any enterprise whose
#       enterprise ID started with 9 (example Nokia(94))being handled as traps
#       from Cisco.
#
# 2.0 - Complete re-write for compatibility with snmp.rules 2.0.  Complies with
#       the Micromuse Rules File Standards (MUSE-STD-RF-01, May 2002)
#
###############################################################################

else if (nmatch($enterprise, ".1.3.6.1.4.1.9."))
{
    log(DEBUG, "<<<<< Entering... cisco.generic.include.snmptrap.rules >>>>>")

    @Agent = "Generic-Cisco"
    @Class = "40057"
    
    switch($generic-trap)
    {
        case "0"|"1": ### coldStart, warmStart
                            
            ##########
            # $1 = sysUpTime - The time (in hundredths of a second) since the
            #        network management portion of the system was last
            #        re-initialized.
            # $2 = whyReload - This variable contains a printable octet string
            #        which contains the reason why the system was last
            #        restarted.
            ##########
        
            $sysUptime = $1
            $whyReload = $2
            details($sysUptime,$whyReload)
                            
            @Summary = @Summary + ": " + $2
            @Identifier = @Identifier + " " + $2
                        
        case "2"|"3": ### linkDown, linkUp
                            
            if(nmatch($OID4, "1.3.6.1.2.1.31.1.1.1"))
            {
                ##########
                # $1 = ifIndex - A unique value for each interface. Its value
                #        ranges between 1 and the value of ifNumber. The value
                #        for each interface must remain constant at least from
                #        one re-initialization of the entity's network
                #        management system to the next re-initialization.
                # $2 = ifAdminStatus - The desired state of the interface. The
                #        testing(3) state indicates that no operational packets
                #        can be passed.
                # $3 = ifOperStatus - The current operational state of the
                #        interface. The testing(3) state indicates that no
                #        operational packets can be passed.
                # $4 = ifName - The textual name of the interface. The value of
                #        this object should be the name of the interface as
                #        assigned by the local device and should be suitable
                #        for use in commands entered at the device's `console'.
                #        This might be a text name, such as `le0' or a simple
                #        port number, such as `1', depending on the interface
                #        naming syntax of the device. If several entries in the
                #        ifTable together represent a single interface as named
                #        by the device, then each will have the same value of
                #        ifName. Note that for an agent which responds to SNMP
                #        queries concerning an interface on some other
                #        (proxied) device, then the value of ifName for such an
                #        interface is the proxied device's local name for it.
                #        If there is no local name, or this object is otherwise
                #        not applicable, then this object contains a
                #        zero-length string.#
                ##########
                            
                if(regmatch($2, "^.*[A-Za-z].*$")||regmatch($3, "^.*[A-Za-z].*$"))
                {
                    $MIBFileNotNull = 1
                }
                            
                $ifIndex = $1
                $ifAdminStatus = lookup($2, ifAdminStatus) + " ( " + $2 + " )"
                $ifOperStatus = lookup ($3, ifOperStatus) + " ( " + $3 + " )"
                $ifName = $4
                details($ifIndex,$ifAdminStatus,$ifOperStatus,$ifName)
                                
                switch($2)
                {
                    case "2": ### Down
                        @Summary = @Summary + ", Administratively  ( " + $4 + " )"
                        @Severity = 2
                    case "3": ### Testing - in some test mode
                        @Summary = @Summary + ", Testing  ( " + $4 + " )"
                        @Severity = 2
                    default:
                        @Summary = @Summary + "  ( " + $4 + " )"
                }
            }
            else if(!exists($4))
            {
                ##########
                # $1 = ifIndex - A unique value for each interface. Its value
                #        ranges between 1 and the value of ifNumber. The value
                #        for each interface must remain constant at least from
                #        one re-initialization of the entity's network
                #        management system to the next re-initialization.
                # $2 = ifDescr - A textual string containing information about
                #        the interface. This string should include the name of
                #        the manufacturer, the product name and the version of
                #        the hardware interface.
                # $3 = ifType - The type of interface, distinguished according
                #        to the physical/link protocol(s) immediately `below'
                #        the network layer in the protocol stack.
                ##########
                            
                if(regmatch($3, "^.*[A-Za-z].*$"))
                {
                    $MIBFileNotNull = 1
                }
                            
                $ifIndex = $1
                $ifDescr = $2
                $ifType = lookup($3, IANAifType) + " ( " + $3 + " )"
                details($ifIndex,$ifDescr,$ifType)
                
                @Summary = @Summary + "  ( " + $2 + " )"
            }
            else
            {
                ##########
                # $1 = ifIndex - A unique value for each interface. Its value
                #        ranges between 1 and the value of ifNumber. The value
                #        for each interface must remain constant at least from
                #        one re-initialization of the entity's network
                #        management system to the next re-initialization.
                # $2 = ifDescr - A textual string containing information about
                #        the interface. This string should include the name of
                #        the manufacturer, the product name and the version of
                #        the hardware interface.
                # $3 = ifType - The type of interface, distinguished according
                #        to the physical/link protocol(s) immediately `below'
                #        the network layer in the protocol stack.
                # $4 = locIfReason - Reason for interface last status change.
                ##########
                            
                if(regmatch($3, "^.*[A-Za-z].*$"))
                {
                    $MIBFileNotNull = 1
                }
                            
                $ifIndex = $1
                $ifDescr = $2
                $ifType = lookup($3, IANAifType) + " ( " + $3 + " )"
                $locIfReason = $4
                details($ifIndex,$ifDescr,$ifType,$locIfReason)
                
                @Summary = @Summary + ": " + $4 + "  ( " + $2 + " )"
                switch($4)
                {
                    case "administratively down":
                        @Severity = 2
                    default:
                }
                @Identifier = @Identifier + " " + $4
            }
        
        case "4": ### authenticationFailure
                            
            ##########
            # $1 = authAddr - This variable contains the last SNMP
            #        authorization failure IP address.
            ##########
                           
            $authAddr = $1
            details($authAddr)
                            
            @AlertKey = $1
            @Summary = @Summary + "  ( from " + $1 + " )"
            @Identifier = @Identifier + " " + $1
                       
        case "5": ### egpNeighborLoss
                        
            ##########
            # $1 = egpNeighAddr - The IP address of this entry's EGP neighbor.
            ###########
                            
            $egpNeighAddr = $1
            details($egpNeighAddr)
                            
            @AlertKey = $1
            @Summary = @Summary + "  ( " + $1 + " )"
            @Identifier = @Identifier + " " + $1
                        
        default:
    }
    
    ### The following statements add the Cisco Model information to the Agent Field
    
    switch(extract($enterprise, "\.1\.3\.6\.1\.4\.1\.9\.([1|5])\.[0-9]+.*"))
    {
        case "1": ### from CISCO-PRODUCTS-MIB
            @Agent = @Agent + " " + lookup(extract($enterprise, "\.1\.3\.6\.1\.4\.1\.9\.1\.([0-9]+).*"), ciscoProducts)
        case "5": ### from CISCO-STACK-MIB
            @Agent = @Agent + " " + lookup(extract($enterprise, "\.1\.3\.6\.1\.4\.1\.9\.5\.([0-9]+).*"), workgroupProducts)
        default:
    }
    
    log(DEBUG, "<<<<< Leaving... cisco.generic.include.snmptrap.rules >>>>>")
}
