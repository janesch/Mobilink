##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-ZS-MIB.include.snmptrap.rules 2.1 
#    2005/03/06 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-ZS-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.294.1.4": ### Cisco Zone Server - Notifications from CISCO-ZS-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-ZS-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Zone Server"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### zoneServiceReqRejNotify

            ##########
            # $1 = zoneMemberFormat
            # $2 = zoneMemberID 
            # $3 = zoneServiceRejReasonCode
            # $4 = zoneServiceRejReasonCodeExp 
            ##########

            $zoneMemberFormat = lookup($1, ciscoZoneMemberType) + " ( " + $1 + " )"
            $zoneMemberID = $2
            $zoneServiceRejReasonCode = lookup($3, FcGs3RejectReasonCode)
            $zoneServiceRejReasonCodeExp = $4
            $vsanIndex = extract($OID1, "\.([0-9]+)\.[0-9]+\.[0-9]+\.[0-9]+$")
            $zoneMemberTypeIndexRaw = extract($OID1, "\.[0-9]+\.([0-9]+)\.[0-9]+\.[0-9]+$")
            $zoneMemberTypeIndex = lookup($zoneMemberTypeIndexRaw, ciscoZoneMemberTypeIndex) + " ( " + $zoneMemberTypeIndexRaw + " )"
            $zoneMemberParentIndex = extract($OID1, "\.[0-9]+\.[0-9]+\.([0-9]+)\.[0-9]+$")
            $zoneMemberIndex = extract($OID1, "\.[0-9]+\.[0-9]+\.[0-9]+\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ZS-MIB-zoneServiceReqRejNotify"

            @AlertGroup = "Registration Request Status"
            @AlertKey = "zoneMemberEntry." + extract($OID1, "\.([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)$")
            @Summary = "Registration Request Rejected by Local Zone Server, " + $zoneServiceRejReasonCode + "  ( " + $zoneMemberFormat + ": " + $2 + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_OS_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3
            
            $zoneMemberFormat = $zoneMemberFormat + " ( " + $1 + " )"
            $zoneServiceRejReasonCode = $zoneServiceRejReasonCode + " ( " + $3 + " )"
            details($zoneMemberFormat,$zoneMemberID,$zoneServiceRejReasonCode,$zoneServiceRejReasonCodeExp,$vsanIndex,$zoneMemberTypeIndex,$zoneMemberParentIndex,$zoneMemberIndex)

        case "2": ### zoneMergeFailureNotify

            ##########
            # $1 = ifIndex
            # $2 = zoneMergeFailureVSANNum
            ##########

            $ifIndex = $1
            $zoneMergeFailureVSANNum = $2
            details($ifIndex,$zoneMergeFailureVSANNum)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ZS-MIB-zoneMergeFailureNotify"

            @AlertGroup = "Zone Merge Status"
            @AlertKey = "ifEntry." + $1 + ", vsanEntry." + $2
            @Summary = "Zone Merge Failure  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "3": ### zoneMergeSuccessNotify

            ##########
            # $1 = ifIndex
            # $2 = zoneMergeSuccessVSANNum
            ##########

            $ifIndex = $1
            $zoneMergeSuccessVSANNum = $2
            details($ifIndex,$zoneMergeSuccessVSANNum)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ZS-MIB-zoneMergeSuccessNotify"

            @AlertGroup = "Zone Merge Status"
            @AlertKey = "ifEntry." + $1 + ", vsanEntry." + $2
            @Summary = "Zone Merge Successful  ( " + @AlertKey + " )"
            
            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "4": ### zoneDefZoneBehaviourChngNotify

            ##########
            # $1 = zoneDefaultZoneBehaviour
            ##########

            $zoneDefaultZoneBehaviour = lookup($1, ciscoZoneDefaultZoneBehaviour)
            $vsanIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-cisco-CISCO-ZS-MIB-zoneDefZoneBehaviourChngNotify"

            @AlertGroup = "Default Zone Behaviour"
            @AlertKey = "zoneDefaultZoneEntry." + $vsanIndex
            @Summary = "Default Zone Behaviour Changed to " + $zoneDefaultZoneBehaviour
            if(int($vsanIndex)<10)
            {
                @Summary = @Summary + "  ( VSAN: VSAN000" + $vsanIndex + " )"
            }
            else if(int($vsanIndex)>9 && int($vsanIndex)<100)
            {
                @Summary = @Summary + "  ( VSAN: VSAN00" + $vsanIndex + " )"
            }
            else if(int($vsanIndex)>99 && int($vsanIndex)<1000)
            {
                @Summary = @Summary + "  ( VSAN: VSAN0" + $vsanIndex + " )"
            }
            else if(int($vsanIndex)>999 && int($vsanIndex)<10000)
            {
                @Summary = @Summary + "  ( VSAN: VSAN" + $vsanIndex + " )"
            }
            else
            {
                @Summary = @Summary + "  ( " + @AlertKey + " )"
            }
            
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 13
            $DEFAULT_ExpireTime = 1800
            
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $zoneDefaultZoneBehaviour = $zoneDefaultZoneBehaviour + " ( " + $1 + " )"
            details($zoneDefaultZoneBehaviour,$vsanIndex)
            
            update(@Summary)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-ZS-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-ZS-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ZS-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-ZS-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-ZS-MIB.include.snmptrap.rules >>>>>")
