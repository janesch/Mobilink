###############################################################################
#
#    Licensed Materials - Property of IBM
#    5724-O48
#
#    (C) Copyright IBM Corp. 2007
#    All Rights Reserved
#
#    US Government Users Restricted Rights - Use, duplication or
#    disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
#
#    huawei-HUAWEI-M8070-MIB.include.snmptrap.rules 3.0
#    2007/05/07 Gerry van der Zanden
#
###############################################################################
#
# 3.0 - Updated release for NIM-03
#
#          -  Supports "Advanced" and "User" include files
#          -  Supports "Severity" lookup tables
#
###############################################################################
#
# 2.0 - Updated release
#
#          -  Repackage for NIM-02
#          -  HUAWEI-M8070-MIB release V1.0
#          -  HUAWEI-NEDevice-MIB release V1.0
#
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/OMNIbus 3.6 and 7.x
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#          -  HUAWEI-M8070-MIB release V1.0
#          -  HUAWEI-NEDevice-MIB release V1.0
#
###############################################################################

case ".1.3.6.1.4.1.2011.2.2.8070": ### Huawei - Traps from HUAWEI-M8070-MIB
                                   ###          (specific traps 1-2)
                                   ### Huawei - Traps from HUAWEI-NEDevice-MIB
                                   ###          (specific traps 3-7)

    log(DEBUG, "<<<<< Entering... huawei-HUAWEI-M8070-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Huawei NE-Series"
    @Class = "40579"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### hwNESlotDown

            ##########
            # The SNMP trap that is generated when a slot is DOWN.
            #
            # $1 = hwNESlotNum - the number of the Slot.
            # $2 = hwNESlotType - The type of the slot.
            ##########

            $hwNESlotNum = $1
            $hwNESlotType = lookup($2,hwNESlotType) 

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-M8070-MIB-hwNESlotDown"

            @AlertGroup = "NE Slot Up/Down"
            $hwNESlotIndex = extract($OID1,"\.([0-9]+)$")
            @AlertKey = "hwNESlotEntry." + $hwNESlotIndex
            @Summary = "Slot Down  ( Slot: " + $1 + ", Type: " + $hwNESlotType + " )"
            update(@Summary)

            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            $hwNESlotType = $hwNESlotType + " ( " + $2 + " )"
            details($hwNESlotNum,$hwNESlotType)

        case "2": ### hwNESlotUp

            ##########
            # The SNMP trap that is generated when a slot is UP.
            #
            # $1 = hwNESlotNum - the number of the Slot.
            # $2 = hwNESlotType - The type of the slot.
            ##########

            $hwNESlotNum = $1
            $hwNESlotType = lookup($2,hwNESlotType)

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-M8070-MIB-hwNESlotUp"

            @AlertGroup = "NE Slot Up/Down"
            $hwNESlotIndex = extract($OID1,"\.([0-9]+)$")
            @AlertKey = "hwNESlotEntry." + $hwNESlotIndex
            @Summary = "Slot Up  ( Slot: " + $1 + ", Type: " + $hwNESlotType + " )"
            update(@Summary)

            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap
            
            $hwNESlotType = $hwNESlotType + " ( " + $2 + " )"
            details($hwNESlotNum,$hwNESlotType)

        case "3": ### powerFail

            ##########
            # The SNMP trap that is generated when a power fails or draws.
            #
            # $1 = powerSlotNum - slot number of the power.
            # $2 = powerType - The product identification of the power.
            ##########

            $powerSlotNum = $1
            $powerType = lookup($2,powerType)

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-NEDevice-MIB-powerFail"

            @AlertGroup = "Power Status"
            @AlertKey = "powerEntry." + $powerSlotNum
            @Summary = "Power Failure  ( Power: " + $1 + ", Type: " + $powerType + " )"
            update(@Summary)

            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            $powerType = $powerType + " ( " + $2 + " )"
            details($powerSlotNum,$powerType)

        case "4": ### powerPlug

            ##########
            # The SNMP trap that is generated when a power normally insert.
            #
            # $1 = powerSlotNum - slot number of the power.
            # $2 = powerType - The product identification of the power :.
            ##########

            $powerSlotNum = $1
            $powerType = lookup($2,powerType)

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-NEDevice-MIB-powerPlug"

            @AlertGroup = "Power Status"
            @AlertKey = "powerEntry." + $powerSlotNum
            @Summary = "End of Power Failure  ( Power: " + $1 + ", Type: " + $powerType + " )"
            update(@Summary)

            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            $powerType = $powerType + " ( " + $2 + " )"
            details($powerSlotNum,$powerType)

        case "5": ### fanFail

            ##########
            # The SNMP trap that is generated when a fan fails or draws.
            #
            # $1 = powerSlotNum - slot number of the power.
            ##########

            $powerSlotNum = $1
            details($powerSlotNum)

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-NEDevice-MIB-fanFail"

            @AlertGroup = "Fan Status"
            @AlertKey = "powerEntry." + $powerSlotNum
            @Summary = "Fan Failure  ( Fan: " + $1 + " )"

            $DEFAULT_Severity = 4
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "6": ### fanStart

            ##########
            # The SNMP trap that is generated when a fan normally run.
            #
            # $1 = powerSlotNum - slot number of the power.
            ##########

            $powerSlotNum = $1
            details($powerSlotNum)

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-NEDevice-MIB-fanStart"

            @AlertGroup = "Fan Status"
            @AlertKey = "powerEntry." + $powerSlotNum
            @Summary = "End of Fan Failure  ( Fan: " + $1 + " )"

            $DEFAULT_Severity = 1
            $DEFAULT_Type = 2
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        case "7": ### hwNEBuffull

            ##########
            # The SNMP trap that is generated when a buffer is used up.
            #
            # $1 = hwNEBufSlotNum - The number of the slot on which the buffer is
            # $2 = hwNEBufSize - The size of this kind of buffer,the unit is byte.
            # $3 = hwNEBufUsed - The used number of this kind of buffer.
            ##########

            $hwNEBufSlotNum = $1
            $hwNEBufSize = $2
            $hwNEBufUsed = $3
            details($hwNEBufSlotNum,$hwNEBufSize,$hwNEBufUsed)

            $OS_EventId = "SNMPTRAP-huawei-HUAWEI-NEDevice-MIB-hwNEBuffull"

            @AlertGroup = "Buffer Status"
            @AlertKey = "hwNEBufEntry." + $hwNEBufSlotNum + "." + $hwNEBufSize
            @Summary = "Buffer Full - Size: " + $2 + " bytes, Used: " + $3 + " bytes  ( Slot: " + $1 + " )"
            update(@Summary)

            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

    ##########
    # Handle Severity via Lookup.
    ##########

    switch($specific-trap)
    {
        case "1"|"2":

            if(exists($SEV_KEY))
            {
                [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, huawei-HUAWEI-M8070-MIB_sev)
            }
            else
            {
                [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, huawei-HUAWEI-M8070-MIB_sev)
            }

        case "3"|"4"|"5"|"6"|"7":

            if(exists($SEV_KEY))
            {
                [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, huawei-HUAWEI-NEDevice-MIB_sev)
            }
            else
            {
                [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, huawei-HUAWEI-NEDevice-MIB_sev)
            }

        default:
    }

    include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"
    
    ##########
    # End of Severity via Lookup.
    ##########

    ##########
    # Enter "Advanced" and "User" includes.
    ##########

    include "$NC_RULES_HOME/include-snmptrap/huawei/huawei-HUAWEI-M8070-MIB.adv.include.snmptrap.rules"
    include "$NC_RULES_HOME/include-snmptrap/huawei/huawei-HUAWEI-M8070-MIB.user.include.snmptrap.rules"

    ##########
    # End of "Advanced" and "User" includes.
    ##########

    log(DEBUG, "<<<<< Leaving... huawei-HUAWEI-M8070-MIB.include.snmptrap.rules >>>>>")
