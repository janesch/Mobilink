###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    IETF-EFM-CU-MIB.include.snmptrap.rules 1.0
#    2006/12/07 Alka Girdhar
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  EFM-CU-MIB
#
###############################################################################

case ".1.3.6.1.2.1.160.1.1": ### Manage Ethernet in the First Mile (EFM) Copper (EFMCu) Interfaces - efmCuPortNotifications from EFM-CU-MIB (20051024) 

    log(DEBUG, "<<<<< Entering... IETF-EFM-CU-MIB.include.snmptrap.rules >>>>>")

    @Agent = "IETF-EFM-CU-MIB"
    @Class = "40086"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### efmCuLowBandwidth

            ##########
            # $1 = ifSpeed
            # $2 = efmCuThreshLowBandwidth
            ##########

            $ifSpeed = $1
            $efmCuThreshLowBandwidth = $2
	    $ifIndex = extract($OID2, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-EFM-CU-MIB-efmCuLowBandwidth"

            @AlertGroup = "EFMCu Port's Bandwidth Status"
            @AlertKey = "efmCuPortConfEntry." + $ifIndex
            @Summary = "Bandwidth Degraded  ( INDEX : " + $ifIndex + " )"

            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($ifSpeed,$efmCuThreshLowBandwidth,$ifIndex)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

    ##########
    # Handle Severity via Lookup.
    ##########

    if(exists($SEV_KEY))
    {
        [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, IETF-EFM-CU-MIB_sev)
    }
    else
    {
        [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-EFM-CU-MIB_sev)
    }

    include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

    ##########
    # End of Severity via Lookup.
    ##########

    ##########
    # Enter "Advanced" and "User" includes.
    ##########

    include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-EFM-CU-MIB_PORT.adv.include.snmptrap.rules"
    include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-EFM-CU-MIB_PORT.user.include.snmptrap.rules"

    ##########
    # End of "Advanced" and "User" includes.
    ##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... IETF-EFM-CU-MIB.include.snmptrap.rules >>>>>")

case ".1.3.6.1.2.1.160.1.2": ### Manage Ethernet in the First Mile (EFM) Copper (EFMCu) Interfaces - efmCuPmeNotifications from EFM-CU-MIB (20051024) 

    log(DEBUG, "<<<<< Entering... IETF-EFM-CU-MIB.include.snmptrap.rules >>>>>")

    @Agent = "IETF-EFM-CU-MIB"
    @Class = "40086"

    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### efmCuPmeLineAtnCrossing

            ##########
            # $1 = efmCuPmeLineAtn 
            # $2 = efmCuPmeThreshLineAtn 
            ##########

            $efmCuPmeLineAtn = $1
            $efmCuPmeThreshLineAtn = $2
	    $ifIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-EFM-CU-MIB-efmCuPmeLineAtnCrossing"

            @AlertGroup = "Loop Attenuation Threshold"
            @AlertKey = "efmCuPmeStatusEntry." + $ifIndex

	    ####
	    # efmCuPmeLineAtnCrossing alarm indicates the current loop 
            # attenuation threshold. This alarm shall be generated in 
            # the following cases:
	    # 
            # a) Current loop attenuation threshold value reach/exceeds the 
	    # limit i.e. efmCuPmeLineAtn value is greater than or equal to
	    # efmCuPmeThreshLineAtn value
	    #
            # b) Current loop attenuation threshold value returns to normal 
            # i.e. efmCuPmeLineAtn value is less than efmCuPmeThreshLineAtn
	    #
	    # As mentioned in the MIB that if the value of efmCuPmeLineAtn is 
	    # 65535, then either PME is down or initializing. In real
            # scenario whenever this event is generated, the efmCuPmeLineAtn
            # value should never be equal to 65535. But there is no harm in 
            # adding it.
	    ####

	    if((int($efmCuPmeLineAtn) == 65535)) ### PME is down or initializing
	    {
                $SEV_KEY = $OS_EventId + "_down" 
                @Summary = "PME Down or Initializing"

                $DEFAULT_Severity = 2
                $DEFAULT_Type = 13
		$DEFAULT_ExpireTime = 1800
	    }
	    else if((int($efmCuPmeLineAtn) < int($efmCuPmeThreshLineAtn))) ### normal 
	    {
                $SEV_KEY = $OS_EventId + "_normal" 
                @Summary = "Current Loop Attenuation Threshold Normal  ( " + @AlertKey + " )" 

                $DEFAULT_Severity = 1
                $DEFAULT_Type = 2
        	$DEFAULT_ExpireTime = 0
	    }
	    else if((int($efmCuPmeLineAtn) >= int($efmCuPmeThreshLineAtn))) ### exceeds/reaches threshold. 
            {
                $SEV_KEY = $OS_EventId + "_exceeds"
                @Summary = "Current Loop Attention Threshold Exceeded/Reached  ( " + @AlertKey + " )" 

                $DEFAULT_Severity = 3 
                $DEFAULT_Type = 1
	        $DEFAULT_ExpireTime = 0
            }

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($efmCuPmeLineAtn,$efmCuPmeThreshLineAtn,$ifIndex)

        case "2": ### efmCuPmeSnrMgnCrossing

            ##########
            # $1 = efmCuPmeSnrMgn 
            # $2 = efmCuPmeThreshSnrMgn 
            ##########

            $efmCuPmeSnrMgn = $1
            $efmCuPmeThreshSnrMgn = $2
            $ifIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-EFM-CU-MIB-efmCuPmeSnrMgnCrossing"

            @AlertGroup = "SNR Margin Threshold"
            @AlertKey = "efmCuPmeStatusEntry." + $ifIndex

            ####
            # efmCuPmeSnrMgnCrossing alarm indicates the current SNR
            # margin threshold. This alarm shall be generated in the 
            # the following cases:
            #
            # a) Current SNR Margin threshold value reach/exceeds the
            # limit i.e. efmCuPmeSnrMgn value is greater than or equal to
            # efmCuPmeThreshSnrMgn value
            #
            # b) Current loop attenuation threshold value drops
            # i.e. efmCuPmeSnrMgn value is less than efmCuPmeThreshSnrMgn
            #
            # As mentioned in the MIB that if the value of efmCuPmeSnrMgn is
            # 65535, then either PME is dowm or initializing. In real
            # scenario whenever this event is generated, the  efmCuPmeSnrMgn value
            # should never be equal to 65535. But there is no harm in adding it.

            if((int($efmCuPmeSnrMgn) == 65535)) ### PME is down or intializing
            {
                $SEV_KEY = $OS_EventId + "_down" 
                @Summary = "PME Down or Initializing"

                $DEFAULT_Severity = 2
                $DEFAULT_Type = 13
		$DEFAULT_ExpireTime = 1800
            }
            else if((int($efmCuPmeSnrMgn) < int($efmCuPmeThreshSnrMgn))) ### drops
            {
                $SEV_KEY = $OS_EventId + "_drops"
                @Summary = "Current SNR Margin Threshold Dropped  ( " + @AlertKey + " )"

                $DEFAULT_Severity = 3
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
            else if((int($efmCuPmeSnrMgn) >= int($efmCuPmeThreshSnrMgn))) ### normal
            {
                $SEV_KEY = $OS_EventId + "_normal"
                @Summary = "Current SNR Margin Threshold Normal  ( " + @AlertKey + " )"

                $DEFAULT_Severity = 1
                $DEFAULT_Type = 2
                $DEFAULT_ExpireTime = 0
            }

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($efmCuPmeSnrMgn,$efmCuPmeThreshSnrMgn,$ifIndex)

        case "3": ### efmCuPmeDeviceFault

            ##########
            # $1 = efmCuPmeFltStatus 
	    #      Possibles values for this object are
	    # 	   0 - Loss Of Framing
	    #	   1 - SNR Margin Dropped
	    #	   2 - Line Attenuation Threshold exceeds
	    # 	   3 - Device Fault
	    #	   4 - Configuration Initialization Failure
	    #	   5 - Protocol Initialization Failure
            ##########

            $efmCuPmeFltStatus = lookup($1, efmCuPmeFltStatus) + " ( " + $1 + " )"
	    $ifIndex = extract($OID1, "\.([0-9]+)$")

            $OS_EventId = "SNMPTRAP-IETF-EFM-CU-MIB-efmCuPmeDeviceFault"

            @AlertGroup = "PME Link Fault Status"
            @AlertKey = "efmCuPmeStatusEntry." + $ifIndex

	    switch($1)
	    {
	        case "1": ### lossOfFraming
	            $SEV_KEY = $OS_EventId + "_lossOfFraming"
                    @Summary = "PME Loss Of Framing"

                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1 
                    $DEFAULT_ExpireTime = 0
	        case "2": ### snrMgnDefect
	            $SEV_KEY = $OS_EventId + "_snrMgnDefect"
                    @Summary = "PME SNR Margin Dropped"

                    $DEFAULT_Severity = 3 
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
	        case "4": ### lineAtnDefect
	            $SEV_KEY = $OS_EventId + "_lineAtnDefect"
                    @Summary = "PME Line Attenuation Threshold Exceeded" 

                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
	        case "8": ### deviceFault
	            $SEV_KEY = $OS_EventId + "_deviceFault"
                    @Summary = "PME Vendor-dependent Diagnostic or Self-test Fault" 
                  
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
	        case "16": ### configInitFailure
	            $SEV_KEY = $OS_EventId + "_configInitFailure"
                    @Summary = "PME Configuration Initialization Failure Detected"

                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
	        case "32": ### protocolInitFailure
	            $SEV_KEY = $OS_EventId + "_protocolInitFailure"
                    @Summary = "PME Protocol Initiatization Failure" 

                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
	        default: 
	            $SEV_KEY = $OS_EventId + "_unknown"
                    @Summary = "PME Unknown Failure" 

                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
	    }  

	    @Summary = @Summary +  " ( " + @AlertKey + " )" 
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1

            details($efmCuPmeFltStatus,$ifIndex)

        case "4": ### efmCuPmeConfigInitFailure

            ##########
            # $1 = efmCuPmeFltStatus 
            # $2 = efmCuAdminProfile 
            # $3 = efmCuPmeAdminProfile 
            ##########

            $efmCuPmeFltStatus = lookup($1, efmCuPmeFltStatus) + " ( " + $1 + " )"
            $efmCuAdminProfile = $2
            $efmCuPmeAdminProfile = $3
            $ifIndex = extract($OID1, "\.([0-9]+)$" )

            $OS_EventId = "SNMPTRAP-IETF-EFM-CU-MIB-efmCuPmeConfigInitFailure"

            @AlertGroup = "PME Link Fault Status"
            @AlertKey = "efmCuPmeStatusEntry." + $ifIndex
            @Summary = "PME Configuration Initialization Failure  ( " + @AlertKey + " )"

            $DEFAULT_Severity = 3 
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($efmCuPmeFltStatus,$efmCuAdminProfile,$efmCuPmeAdminProfile,$ifIndex)

        case "5": ### efmCuPmeProtocolInitFailure

            ##########
            # $1 = efmCuPmeFltStatus 
            # $2 = efmCuPmeOperSubType 
            ##########

            $efmCuPmeFltStatus = lookup($1, efmCuPmeFltStatus) + " ( " + $1 + " )"
            $efmCuPmeOperSubType = lookup($2, efmCuPmeOperSubType) + " ( " + $2 + " )"
            $ifIndex = extract($OID1, "\.([0-9]+)$" )

            $OS_EventId = "SNMPTRAP-IETF-EFM-CU-MIB-efmCuPmeProtocolInitFailure"

            @AlertGroup = "PME Link Fault Status"
            @AlertKey = "efmCuPmeStatusEntry." + $ifIndex 
            @Summary = "PME Protocol Initiatization Failure  ( " + @AlertKey + " )"

            $DEFAULT_Severity = 3
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0

            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($efmCuPmeFltStatus,$efmCuPmeOperSubType,$ifIndex)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

    ##########
    # Handle Severity via Lookup.
    ##########

    if(exists($SEV_KEY))
    {
       [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, IETF-EFM-CU-MIB_sev)
    }
    else
    {
       [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, IETF-EFM-CU-MIB_sev)
    }

    include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

    ##########
    # End of Severity via Lookup.
    ##########

    ##########
    # Enter "Advanced" and "User" includes.
    ##########

    include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-EFM-CU-MIB_PME.adv.include.snmptrap.rules"
    include "$NC_RULES_HOME/include-snmptrap/IETF/IETF-EFM-CU-MIB_PME.user.include.snmptrap.rules"

    ##########
    # End of "Advanced" and "User" includes.
    ##########

    include "$NC_RULES_HOME/include-common/load_include.rules"

    log(DEBUG, "<<<<< Leaving... IETF-EFM-CU-MIB.include.snmptrap.rules >>>>>")
