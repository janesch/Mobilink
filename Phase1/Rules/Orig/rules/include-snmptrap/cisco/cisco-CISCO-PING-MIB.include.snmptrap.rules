###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-PING-MIB.include.snmptrap.rules 1.0
#    2004/03/24 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-PING-MIB
#
###############################################################################

case ".1.3.6.1.4.1.9.9.16.2": ### Cisco Ping - Notifications from CISCO-PING-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-PING-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Ping"
    @Class = "40057"
   
    $OPTION_TypeFieldUsage = "3.6"
 
    switch ($specific-trap)
    {
        case "1": ### ciscoPingCompletion
        
            ##########
            # $1 = ciscoPingCompleted
            # $2 = ciscoPingSentPackets 
            # $3 = ciscoPingReceivedPackets
            ##########
        
            $ciscoPingCompleted = $1
            $ciscoPingSentPackets = $2
            $ciscoPingReceivedPackets = $3

            $OS_EventId = "SNMPTRAP-cisco-CISCO-PING-MIB-ciscoPingCompletion"
            @AlertGroup = "Ping Completion Status"
            $ciscoPingSerialNumber = extract($OID1, "\.([0-9]+)$")
            @AlertKey = "ciscoPingEntry." + $ciscoPingSerialNumber

            if( int($ciscoPingSentPackets) == int($ciscoPingReceivedPackets) )
            {
                @Summary = "Ping Completetion Successful: SentPackets = " + $2 + ", ReceivedPackets = " + $3
                $SEV_KEY = $OS_EventId + "_successful"
                $DEFAULT_Severity = 1
                $DEFAULT_Type = 2
                $DEFAULT_ExpireTime = 0
            }

            else if( int($ciscoPingReceivedPackets) == 0)
            {
                @Summary = "Ping Completion Failed : SentPackets = " + $2  + ", ReceivedPackets = " + $3
                $SEV_KEY = $OS_EventId + "_failed"
                $DEFAULT_Severity = 5
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }

           else if( int($ciscoPingReceivedPackets) < int($ciscoPingSentPackets))
            {
            	@Summary = "Ping Completion In Progress : SentPackets = " + $2 + ", ReceivedPackets = " + $3
            	$SEV_KEY = $OS_EventId + "_inprogress"
                $DEFAULT_Severity = 1
                $DEFAULT_Type = 2
                $DEFAULT_ExpireTime = 0
             }

           else
            {
            	@Summary = "Ping Completion Problem : SentPackets = " + $2 + ", ReceivedPackets = " + $3
            	$SEV_KEY = $OS_EventId + "_problem"
                $DEFAULT_Severity = 4
                $DEFAULT_Type = 1
                $DEFAULT_ExpireTime = 0
            }
        
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup  + " " + @Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " +$2+ " " +$3
            details($ciscoPingCompleted,$ciscoPingSentPackets,$ciscoPingReceivedPackets)
   
        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-PING-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-PING-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-PING-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-PING-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-PING-MIB.include.snmptrap.rules >>>>>")

