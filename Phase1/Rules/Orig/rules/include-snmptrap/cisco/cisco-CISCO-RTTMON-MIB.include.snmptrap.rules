###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-RTTMON-MIB.include.snmptrap.rules 2.1
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-RTTMON-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.42.2": ### Cisco Response Time Monitor - Notifications from CISCO-RTTMON-MIB and CISCO-RTTMON-MIB-120_5_T

    log(DEBUG, "<<<<< Entering... cisco-CISCO-RTTMON-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Response Time Monitor"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    ##########
    # NOTE: All Cisco Response Time Monitor Traps contain the following
    # variable bindings.
    #
    # $1 = rttMonCtrlAdminTag
    # $2 = rttMonHistoryCollectionAddress
    ##########        

    switch($specific-trap)
    {
        case "1": ### rttMonConnectionChangeNotification

            ##########
            # This notification is only valid when the RttMonRttType is 'echo'
            # or 'pathEcho'. A rttMonConnectionChangeNotification indicates
            # that a connection to a target (not to a hop along the path to a
            # target) has either failed on establishment or been lost and when
            # reestablished. Precisely, this has resulted in
            # rttMonCtrlOperConnectionLostOccurred changing value. If History
            # is not being collected, the instance values for the
            # rttMonHistoryCollectionAddress object will not be valid. When
            # RttMonRttType is not 'echo' or 'pathEcho' the
            # rttMonHistoryCollectionAddress object will be null.
            #
            # $3 = rttMonCtrlOperConnectionLostOccurred 
            ##########

            $rttMonCtrlAdminTag = $1
            $rttMonHistoryCollectionAddress = $2
            $rttMonCtrlOperConnectionLostOccurred = lookup($3, TruthValue) + " ( " + $3 + " )"
            $rttMonCtrlAdminIndex = extract($OID3, "\.([0-9]+)$")
            details($rttMonCtrlAdminTag,$rttMonHistoryCollectionAddress,$rttMonCtrlOperConnectionLostOccurred,$rttMonCtrlAdminIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-RTTMON-MIB-rttMonConnectionChangeNotification"
            
            @AlertGroup = "RTT Connection Status"
            @AlertKey = "rttMonCtrlOperEntry." + $rttMonCtrlAdminIndex
            switch($3)
            {
                case "1": ### true
                    @Summary = "Response Time Connection Failed"
                    
                    $SEV_KEY = $OS_EventId + "_true"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### false
                    @Summary = "Response Time Connection Established"
                    
                    $SEV_KEY = $OS_EventId + "_false"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Response Time Connection Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            if(match($2, ""))
            {
                @Summary = @Summary + "  ( " + @AlertKey + " )"
            }
            else
            {
                @Summary = @Summary + "  ( Target: " + $2 + " )"
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3

        case "2": ### rttMonTimeoutNotification

            ##########
            # $3 = rttMonCtrlOperTimeoutOccurred
            ##########

            $rttMonCtrlAdminTag = $1
            $rttMonHistoryCollectionAddress = $2
            $rttMonCtrlOperTimeoutOccurred = lookup($3, TruthValue) + " ( " + $3 + " )"
            $rttMonCtrlAdminIndex = extract($OID3, "\.([0-9]+)$")
            details($rttMonCtrlAdminTag,$rttMonHistoryCollectionAddress,$rttMonCtrlOperTimeoutOccurred,$rttMonCtrlAdminIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-RTTMON-MIB-rttMonTimeoutNotification"
            
            @AlertGroup = "RTT Operation Status"
            @AlertKey = "rttMonCtrlOperEntry." + $rttMonCtrlAdminIndex
            switch($3)
            {
                case "1": ### true
                    @Summary = "Response Time Operation Timed-Out"
                    
                    $SEV_KEY = $OS_EventId + "_true"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### false
                    @Summary = "Response Time Operation Successful"
                    
                    $SEV_KEY = $OS_EventId + "_false"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Response Time Operation Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            if(match($2, ""))
            {
                @Summary = @Summary + "  ( " + @AlertKey + " )"
            }
            else
            {
                @Summary = @Summary + "  ( Target: " + $2 + " )"
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3

        case "3": ### rttMonThresholdNotification

            ##########
            # $3 = rttMonCtrlOperOverThresholdOccurred 
            ##########

            $rttMonCtrlAdminTag = $1
            $rttMonHistoryCollectionAddress = $2
            $rttMonCtrlOperOverThresholdOccurred = lookup($3, TruthValue) + " ( " + $3 + " )"
            $rttMonCtrlAdminIndex = extract($OID3, "\.([0-9]+)$")
            details($rttMonCtrlAdminTag,$rttMonHistoryCollectionAddress,$rttMonCtrlOperOverThresholdOccurred,$rttMonCtrlAdminIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-RTTMON-MIB-rttMonThresholdNotification"
            
            @AlertGroup = "RTT Threshold Status"
            @AlertKey = "rttMonCtrlOperEntry." + $rttMonCtrlAdminIndex
            switch($3)
            {
                case "1": ### true
                    @Summary = "Response Time Threshold Crossed"
                    
                    $SEV_KEY = $OS_EventId + "_true"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### false
                    @Summary = "Response Time Threshold Normal"
                    
                    $SEV_KEY = $OS_EventId + "_false"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Response Time Threshold Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            if(match($2, ""))
            {
                @Summary = @Summary + "  ( " + @AlertKey + " )"
            }
            else
            {
                @Summary = @Summary + "  ( Target: " + $2 + " )"
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3

        case "4": ### rttMonVerifyErrorNotification

            ##########
            # $3 = rttMonCtrlOperVerifyErrorOccurred
            ##########

            $rttMonCtrlAdminTag = $1
            $rttMonHistoryCollectionAddress = $2
            $rttMonCtrlOperVerifyErrorOccurred = lookup($3, TruthValue) + " ( " + $3 + " )"
            $rttMonCtrlAdminIndex = extract($OID3, "\.([0-9]+)$")
            details($rttMonCtrlAdminTag,$rttMonHistoryCollectionAddress,$rttMonCtrlOperVerifyErrorOccurred,$rttMonCtrlAdminIndex)

            $OS_EventId = "SNMPTRAP-cisco-CISCO-RTTMON-MIB-rttMonVerifyErrorNotification"
            
            @AlertGroup = "RTT Operation Status"
            @AlertKey = "rttMonCtrlOperEntry." + $rttMonCtrlAdminIndex
            switch($3)
            {
                case "1": ### true
                    @Summary = "Response Time Operation Error"
                    
                    $SEV_KEY = $OS_EventId + "_true"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### false
                    @Summary = "Response Time Operation Successful"
                    
                    $SEV_KEY = $OS_EventId + "_false"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Response Time Operation Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            if(match($2, ""))
            {
                @Summary = @Summary + "  ( " + @AlertKey + " )"
            }
            else
            {
                @Summary = @Summary + "  ( Target: " + $2 + " )"
            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $3

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-RTTMON-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-RTTMON-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-RTTMON-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-RTTMON-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-RTTMON-MIB.include.snmptrap.rules >>>>>")
