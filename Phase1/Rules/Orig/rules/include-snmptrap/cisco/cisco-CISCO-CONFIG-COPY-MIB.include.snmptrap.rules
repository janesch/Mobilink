##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-CONFIG-COPY-MIB.include.snmptrap.rules 2.1 
#    2005/03/03 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-CONFIG-COPY-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.9.96.2.1": ### Cisco Router Configuration Copy - Notifications from CISCO-CONFIG-COPY-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-CONFIG-COPY-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-Router Configuration Copy"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"
    
    switch($specific-trap)
    {
        case "1": ### ccCopyCompletion

            ##########
            # $1 = ccCopyServerAddress
            # $2 = ccCopyFileName 
            # $3 = ccCopyState
            # $4 = ccCopyTimeStarted 
            # $5 = ccCopyTimeCompleted 
            # $6 = ccCopyFailCause
            ##########
            
            $ccCopyServerAddress = $1
            $ccCopyFileName = $2
            $ccCopyState = lookup($3, ConfigCopyState) + " ( " + $3 + " )"
            $ccCopyTimeStarted = $4
            $ccCopyTimeCompleted = $5
            $ccCopyFailCause = lookup($6, ConfigCopyFailCause)
            $ccCopyIndex = extract($OID1, "\.([0-9]+)$")
            
            $OS_EventId = "SNMPTRAP-cisco-CISCO-CONFIG-COPY-MIB-ccCopyCompletion"
            
            @AlertGroup = "Configuration Copy Status"
            @AlertKey = "ccCopyEntry." + $ccCopyIndex
            switch($3)
            {
                case "1": ### waiting
                    @Summary = "Configuration Copy Waiting"
                    
                    $SEV_KEY = $OS_EventId + "_waiting"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### running
                    @Summary = "Configuration Copy Running"
                    
                    $SEV_KEY = $OS_EventId + "_running"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 12
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### successful
                    @Summary = "Configuration Copy Successful"
                    
                    $SEV_KEY = $OS_EventId + "_successful"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### failed
                    @Summary = "Configuration Copy Failed, " + $ccCopyFailCause
                    
                    $SEV_KEY = $OS_EventId + "_failed"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "Configuration Copy Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( TFTP Server: " + $1 + ", File: " + $2 +  " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $2
            
            $ccCopyFailCause = $ccCopyFailCause + " ( " + $6 + " )"
            details($ccCopyServerAddress,$ccCopyFileName,$ccCopyState,$ccCopyTimeStarted,$ccCopyTimeCompleted,$ccCopyFailCause,$ccCopyIndex)
        
        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-CONFIG-COPY-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-CONFIG-COPY-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-CONFIG-COPY-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-CONFIG-COPY-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-CONFIG-COPY-MIB.include.snmptrap.rules >>>>>")
