##############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-GATEKEEPER-MIB.include.snmptrap.rules 2.1 
#    2005/03/04 Robert Cowart
#
###############################################################################
###############################################################################
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-GATEKEEPER-MIB
#
###############################################################################
#
# 2.1 - Simplified handling of "Severity via lookup" logic.
#
#     - Added basic debug logging.
#
# 2.0 - Rewritten for improved event presentation.  Includes enhancements which
#       comply with the Netcool Rules File Standards
#       (MUSE-STD-RF-02, July 2002)
#
#     - Modified to support following features introduced in NCiL 2.0:
#         - "Advanced" and "User" include files
#         - "Severity" lookup tables
#
# 1.0 - Based on rules extracted from cisco.include.snmptrap.rules 3.1.
#
###############################################################################

case ".1.3.6.1.4.1.9.10.40.2": ### Cisco H.323 Gatekeeper - Notifications from CISCO-GATEKEEPER-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-GATEKEEPER-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-H.323 Gatekeeper"
    @Class = "40057"
    
    $OPTION_TypeFieldUsage = "3.6"

    switch($specific-trap)
    {
        case "1": ### ciscoGatekeeperEvent

            ##########
            # $1 = cgkHistoryEventType 
            # $2 = cgkHistoryEventEndpointType
            # $3 = cgkHistoryEventEndpointAddrTag 
            # $4 = cgkHistoryEventEndpointAddress 
            # $5 = cgkHistoryEventEndpointH323id
            ##########

            $cgkHistoryEventType = lookup($1, cgkHistoryEventType) + " ( " + $1 + " )"
            $cgkHistoryEventEndpointType = lookup($2, cgkHistoryEventEndpointType)
            $cgkHistoryEventEndpointAddrTag = $3
            $cgkHistoryEventEndpointAddress = $4
            $cgkHistoryEventEndpointH323id = $5

            $OS_EventId = "SNMPTRAP-cisco-CISCO-GATEKEEPER-MIB-ciscoGatekeeperEvent"

            @AlertGroup = "H.323 GateKeeper Status"
            @AlertKey = "cgkHistoryEventEntry." + extract($OID1, "\.([0-9]+)$")
            switch($1)
            {
                case "1": ### other
                    @Summary = "H.323 GateKeeper Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_other"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "2": ### register
                    @AlertGroup = "H.323 Endpoint Registration"
                    @AlertKey = "Endpoint: " + rtrim($4)
                    if(!match($5, ""))
                    {
                        @AlertKey = @AlertKey + ", H.323 ID: " + $5
                    }
                    @Summary = "H.323 " + $cgkHistoryEventEndpointType + " Registered with GateKeeper"
                    
                    $SEV_KEY = $OS_EventId + "_register"
                    $DEFAULT_Severity = 1
                    $DEFAULT_Type = 2
                    $DEFAULT_ExpireTime = 0
                    
                case "3": ### unregister
                    @AlertGroup = "H.323 Endpoint Registration"
                    @AlertKey = "Endpoint: " + rtrim($4)
                    if(!match($5, ""))
                    {
                        @AlertKey = @AlertKey + ", H.323 ID: " + $5
                    }
                    @Summary = "H.323 " + $cgkHistoryEventEndpointType + " Unregistered with GateKeeper"
                    
                    $SEV_KEY = $OS_EventId + "_unregister"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "4": ### unregisterForced
                    @AlertGroup = "H.323 Endpoint Registration"
                    @AlertKey = "Endpoint: " + rtrim($4)
                    if(!match($5, ""))
                    {
                        @AlertKey = @AlertKey + ", H.323 ID: " + $5
                    }
                    @Summary = "H.323 " + $cgkHistoryEventEndpointType + " Unregister Forced by GateKeeper"
                    
                    $SEV_KEY = $OS_EventId + "_unregisterForced"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                case "5": ### overload
                    @Summary = "H.323 GateKeeper Sustained Overload, Calls Rejected"
                    
                    $SEV_KEY = $OS_EventId + "_overload"
                    $DEFAULT_Severity = 3
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
                default:
                    @Summary = "H.323 GateKeeper Status Unknown"
                    
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0
                    
            }
            @Summary = @Summary + "  ( " + @AlertKey + " )"
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + $DEFAULT_Type + " " + @Agent + " " + @Manager + " " + $specific-trap + " " + $1
            
            $cgkHistoryEventEndpointType = $cgkHistoryEventEndpointType + " ( " + $2 + " )"
            details($cgkHistoryEventType,$cgkHistoryEventEndpointType,$cgkHistoryEventEndpointAddrTag,$cgkHistoryEventEndpointAddress,$cgkHistoryEventEndpointH323id)

        default:

            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-GATEKEEPER-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-GATEKEEPER-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-GATEKEEPER-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-GATEKEEPER-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-GATEKEEPER-MIB.include.snmptrap.rules >>>>>")
