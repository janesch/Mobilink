###############################################################################
#
#    Licensed Materials - Property of IBM
#
#    (C) Copyright IBM Corp. 2006
#    All Rights Reserved
#
#    cisco-CISCO-CHANNEL-MIB.include.snmptrap.rules 1.0
#    2004/03/24 Robert Cowart
#
###############################################################################
###############################################################################
#
# 1.0 - Initial Release.
#
#        Compatible with:
#
#          -  Netcool/Omnibus 3.x and 7
#          -  Netcool Rules File Standards (MUSE-STD-RF-02, July 2002)
#
#          -  CISCO-CHANNEL-MIB
#
###############################################################################

case ".1.3.6.1.4.1.9.9.20.1.5": ### Cisco IBM channel - Notifications from CISCO-CHANNEL-MIB

    log(DEBUG, "<<<<< Entering... cisco-CISCO-CHANNEL-MIB.include.snmptrap.rules >>>>>")

    @Agent = "Cisco-IBM channel"
    @Class = "40057"

    $OPTION_TypeFieldUsage = "3.6"
    
    switch ($specific-trap)
    {
        case "1": ### cipCardLinkFailure

            ##########
            # $1 = cipCardDtrBrdIndex 
            # $2 = cipCardDtrBrdStatus 
            # $3 = cipCardDtrBrdSignal
            # $4 = linkIncidentTrapCause
            # $5 = implicitIncidents
            # $6 = codeViolationErrors
            # $7 = linkFailureSignalOrSyncLoss 
            # $8 = linkFailureNOSs
            # $9 = linkFailureSequenceTimeouts
            # $10 = linkFailureInvalidSequences
            ##########
     
            $cipCardDtrBrdIndex = $1
            $cipCardDtrBrdStatus = $2
            $cipCardDtrBrdSignal = $3
            $linkIncidentTrapCause = lookup($4, linkIncidentTrapCause) + " ( " + $4 + " )"
            $implicitIncidents = $5
            $codeViolationErrors = $6
            $linkFailureSignalOrSyncLoss = $7
            $linkFailureNOSs = $8
            $linkFailureSequenceTimeouts = $9
            $linkFailureInvalidSequences = $10

            $cipCardEntryIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
       
            $OS_EventId = "SNMPTRAP-cisco-CISCO-CHANNEL-MIB-cipCardLinkFailure"            

            @AlertGroup = "Card Link Failure"
            @AlertKey = "cipCardDaughterBoardEntry." + $cipCardEntryIndex + "." + $cipCardDtrBrdIndex

            @Summary = "Significant Link Event: Degraded Interface Line Quality"
            $DEFAULT_Severity = 2
            $DEFAULT_Type = 1
            $DEFAULT_ExpireTime = 0
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + @Type + " " + @Agent + " " + @Manager + " " + $specific-trap

            details($cipCardDtrBrdIndex,$cipCardDtrBrdStatus,$cipCardDtrBrdSignal,$linkIncidentTrapCause,$implicitIncidents,$codeViolationErrors,$linkFailureSignalOrSyncLoss,$linkFailureNOSs,$linkFailureSequenceTimeouts,$linkFailureInvalidSequences)
 
        case "2": ### cipCardDtrBrdLinkFailure
        
            ##########
            # $1 = cipCardDtrBrdStatus
            # $2 = cipCardDtrBrdSignal
            # $3 = linkIncidentTrapCause
            ##########
           
            $cipCardDtrBrdStatus = $1
            $cipCardDtrBrdSignal = $2
            $linkIncidentTrapCause = lookup($3, linkIncidentTrapCause)
             
            $OS_EventId = "SNMPTRAP-cisco-CISCO-CHANNEL-MIB-cipCardDtrBrdLinkFailure" 
            
            @AlertGroup = "Card Link Failure"
            $cipCardEntryIndex = extract($OID1, "\.([0-9]+)\.[0-9]+$")
            $cipCardDtrBrdIndex = extract($OID1, "\.([0-9]+)$")

            @AlertKey = "cipCardDaughterBoardEntry." + $cipCardEntryIndex + "." + $cipCardDtrBrdIndex
            switch ($1)
            {
                case "1":
                    switch ($2)
                    {
                        case "1":
                            @Summary = "Board Loaded and Executing, Signal OK: " + $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                            $SEV_KEY = $OS_EventId + "_ok_ok"
                            $DEFAULT_Severity = 1
                            $DEFAULT_Type = 2
                            $DEFAULT_ExpireTime = 0
                        case "2":
                            @Summary = "Board Loaded and Executing, Signal Failure: " +  $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                            $SEV_KEY = $OS_EventId + "_ok_fail"
                            $DEFAULT_Severity = 3
                            $DEFAULT_Type = 1
                            $DEFAULT_ExpireTime = 0
                        default:
                            @Summary = "Board Loaded and Executing, Signal Unknown: " + $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                            $SEV_KEY = $OS_EventId + "_ok_unknown"
                            $DEFAULT_Severity = 2
                            $DEFAULT_Type = 1
                            $DEFAULT_ExpireTime = 0
                    }
                case "2":
                    switch ($2)
                    {
                        case "1":
                            @Summary = "Board Failed, Signal OK: " + $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                            $SEV_KEY = $OS_EventId + "_fail_ok"
                            $DEFAULT_Severity = 3
                            $DEFAULT_Type = 1
                            $DEFAULT_ExpireTime = 0
                        case "2":
                            @Summary = "Board Failed, Signal Failure: " + $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                            $SEV_KEY = $OS_EventId + "_fail_fail"
                            $DEFAULT_Severity = 4
                            $DEFAULT_Type = 1
                            $DEFAULT_ExpireTime = 0
                        default:
                            @Summary = "Board Failed, Signal Failure: " + $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                            $SEV_KEY = $OS_EventId + "_fail_unknown"
                            $DEFAULT_Severity = 2
                            $DEFAULT_Type = 1
                            $DEFAULT_ExpireTime = 0

                    }
                default:
                    @Summary = "Board Loaded and Executing, Signal Unknown: " + $linkIncidentTrapCause + "  ( Card.DtrBrd = " + @AlertKey + " )"
                    $SEV_KEY = $OS_EventId + "_unknown"
                    $DEFAULT_Severity = 2
                    $DEFAULT_Type = 1
                    $DEFAULT_ExpireTime = 0

            }
            @Identifier = @Node + " " + @AlertKey + " " + @AlertGroup + " " + @Type + " " + $specific-trap + " " + $1 + " " + $2 + " " + $3 + " " + @Agent + " " + @Manager

             $linkIncidentTrapCause = $linkIncidentTrapCause + " ( " + $3 + " )"   
             details($cipCardDtrBrdStatus,$cipCardDtrBrdSignal,$linkIncidentTrapCause)
 
        default:
        
            @Summary = "Unknown Specific Trap Number (" + $specific-trap + ") Received for Enterprise " + $enterprise
            @Severity = 1
            @Identifier = @Node + " " + @Agent + " " + @Manager + " " + $enterprise + " " + $generic-trap + " " + $specific-trap
            details($*)
    }

##########
# Handle Severity via Lookup.
##########

if(exists($SEV_KEY))
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($SEV_KEY, cisco-CISCO-CHANNEL-MIB_sev)
}
else
{
    [$OS_Severity,$OS_Type,$OS_ExpireTime] = lookup($OS_EventId, cisco-CISCO-CHANNEL-MIB_sev)
}
include "$NC_RULES_HOME/include-common/AssignSev.include.common.rules"

##########
# End of Severity via Lookup.
##########

##########
# Enter "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-CHANNEL-MIB.adv.include.snmptrap.rules"
include "$NC_RULES_HOME/include-snmptrap/cisco/cisco-CISCO-CHANNEL-MIB.user.include.snmptrap.rules"

##########
# End of "Advanced" and "User" includes.
##########

include "$NC_RULES_HOME/include-common/load_include.rules"

log(DEBUG, "<<<<< Leaving... cisco-CISCO-CHANNEL-MIB.include.snmptrap.rules >>>>>")
